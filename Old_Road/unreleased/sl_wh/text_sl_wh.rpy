label alt_day4_sl_wh_begin:
    scene bg ext_path2_night with dissolve
    play music music_7dl["nowyouseeme"] fadein 3
    play ambience ambience_forest_night fadein 3
    "Недовольный собой, я двигался домой."
    "Честно сказать, я и так сделал куда больше, чем планировал."
    "Никто не ставил передо мной задачи забраться в подземелья и там долго и упорно искать кого-то."
    if alt_day4_sl_cl_tut_iz:
        "Тем более, что у меня и фонарика никакого с собой не было."
    "И всё-таки я был недоволен собой."
    "Была у меня маленькая тщеславная мыслишка, шепчущая о том, как это было бы здорово."
    "Усугубить свой и без того героический статус."
    dreamgirl "Ага. Там, глядишь, и девушки внимание обращать начнут. {w}И Славушка твоя на тебя посмотрит не с чувством товарищества и поддержки."
    th "Мимо, чувак, мимо. {w}Это абсолютно не моя лига, так высоко я не летаю."
    dreamgirl "Как сказать."
    dreamgirl "Это же не современное тебе общество, кастовость здесь отсутствует, а социальные лифты работают только в путь — вспомни хотя бы вчерашний день."
    th "Я просто помогал ей с делами по лагерю, за что и поплатился в итоге."
    dreamgirl "Помощь с антенной в дела по лагерю точно не входила, это была твоя недобрая воля, за что ты и получил абонемент на нерабочие крылья."
    dreamgirl "Остался бы лучше танцевать с девочкой, хорошая девочка, красивая, одинокая."
    th "Сомнительно. {w}Такая красота не может простаивать. Думаю, у неё кто-нибудь…"
    "Я не закончил мысль."
    play sound sfx_scary_sting
    stop music fadeout 3
    pause(1)
    play sound sfx_bush_leaves fadein 0
    "Меня перебил подозрительный шорох кустов."
    play music music_7dl["dead_silence"] fadein 3
    "Очень подозрительный."
    "Я замер."
    play sound sfx_bush_leaves fadein 0
    "Шорох повторился."
    th "Блин, кто это может быть?"
    "Едва ли какой-нибудь опасный хищник, их всё-таки с душой так разгоняют, когда строят заведения для детского отдыха."
    "Мало кому понравится, если в спальню к их спящему отпрыску запрыгнет рысь."
    "Но какой-нибудь заяц-лис вполне, вполне."
    play sound sfx_bush_leaves fadein 0
    "Я направился в сторону подозрительного шума, двигаясь почему-то на цыпочках."
    th "Там кто-то есть!"
    dreamgirl "Догадываюсь!"
    dreamgirl "Будем смотреть или ну его в баню?"
    th "Да страхово мне что-то!"
    dreamgirl "Ну и пошли тогда отсюда!"
    play sound sfx_hiding_in_bush
    "Я заколебался."
    "С одной стороны, что бы там в кустах ни шуршало, вряд ли оно дружелюбное, иначе зачем ему прятаться?"
    "С другой стороны — любопытство кошку сгубило!"
    "В конце концов, что я теряю!"
    play sound sfx_hiding_in_bush
    menu:
        "Проследить за тенью":
            "Я раздвинул кусты, и увидел…"
            play music music_7dl["dropit"] fadein 3
            scene cg d2_slavya_forest with dissolve2
            "Куда-то спешащую Славю."
            "Как и позавчера, она шла, безбоязненно ступая — так, будто прекрасно видела в темноте."
            "Отличие, правда, было в том, что в этот раз мы шли рядом, никто никого не догонял."
            th "Куда это она идёт, интересно?"
            th "Только не надо мне про ежей."
            dreamgirl "Да-да. Любопытство. Кошка."
            dreamgirl "Но что она в лесу среди ночи забыла?!"
            th "Сейчас мы это и узнаем!"
            "Памятуя её прошлый спич о громкости моих перемещений, я маскировался и шёл только тогда, когда шла она."
            "Звуки моих шагов прекрасно заглушались шорохом раздвигаемых кустов."
            "И до поры я мог легко следить за ней, сохраняя безопасную дистанцию."
            "К тому же, помня про материальное давление взгляда, я не смотрел прямо на неё, больше уповая на боковое зрение."
            "Тем более, что оно куда лучше работает в темноте, чем прямой взгляд."
            "Славя же вела себя беззаботно, она чуть ли не подпрыгивала — так, что висящий у неё на плече матерчатый мешочек время от времени хлопал её по спине."
            "По-моему, даже напевала что-то."
            "Я же терялся в догадках."
            "Репутация активистки подсказывала пару самых очевидных вариантов — либо она ходит за каким-нибудь зверёнышем, либо купается, пока никто не видит."
            "Но в той стороне, куда мы направлялись, я совершенно точно не помнил никаких водоёмов."
            "Разве что где-то через десять километров должно было начинаться крохотное болотце, где местные сыроеды пробавлялись морошкой."
            "Но вряд ли она пойдёт так далеко, да ещё и на болото, ночью."
            "В ту сторону была только"
            stop music fadeout 3
            scene bg ext_polyana_night with dissolve
            play sound sfx_hiding_in_bush
            extend " костровая поляна."
            th "Активисточка ходит ночью, чтобы пожечь в лесу костёр?"
            dreamgirl "Я могу, конечно, ошибаться, но, кажется, на завтрашний вечер назначен лагерный костёр."
            dreamgirl "Да, я тоже считаю, что вряд ли она станет отлучаться из лагеря ночью, чтобы подготовить поляну, для этого у неё есть целый день всё-таки."
            "Славя, между тем, вела себя крайне уверенно и деловито."
            "В мешочке у неё обнаружилось несколько палок, пригодных для небольшого костра…{w} И несколько минут спустя огонь был рождён."
            "Походив ещё немного по периметру поляны, она собрала ещё немного хвороста — видимо, про запас."
            "Кажется, её вообще не очень заботило то, что вокруг ночь, что она за территорией, что здесь может случиться что угодно."
            "Но раз она была спокойной, то и я успокоился."
            "Занял удобный наблюдательный пост в распадке между двух низко стелющихся ёлок, из-за которых мог видеть всё, не опасаясь быть обнаруженным."
            "А потом она меня удивила."
            "Закончив свои приготовления, она повернулась лицом к огню и, выкрикнув что-то… Начала расстёгивать рубашку."
            "Я застыл."
            "И тут же разочарованно выдохнул — под пионерской формой у девочки обнаружилась какая-то длинная рубаха со славянским орнаментом по подолу и на воротнике."
            "Понятно, что она больше показывала, чем скрывала, но по сравнению с ожидаемой наготой…"
            "Она достала что-то из мешочка и бросила это в костёр — и тот вспыхнул ярко-голубым!"
            scene bg ext_polyana_night:
                linear 10 zoom 1.1
            with dissolve
            pause(.4)
            scene bg ext_polyana_night:
                zoom 1.1
                linear 10 xalign .8
            with dissolve
            pause(.4)
            scene bg ext_polyana_night:
                zoom 1.1
                linear 10 xalign .2
            with dissolve
            "По поляне поплыли клубы дыма, а очертания предметов исказились и поплыли."
            stop music fadeout 3
            "Я с огромным трудом отгонял от себя ощущение нереальности происходящего — но как можно не верить в чудо, когда это чудо забралось тебе на голову и царапает макушку острыми коготками?"
            "Славя вскинула руки к небу и начала что-то читать гортанным говором, напевным, плавным."
            th "Вот уж не думал, что она верит во всю эту чушь."
            "Вяло подумал я."
            "Внутренний голос промолчал."
            th "Если она сейчас начнёт крутиться или валяться по земле, я совсем в ней разочаруюсь."
            "Отстранённо подумал я."
            with fade
            "Я продолжал скучать, Славя продолжал читать что-то."
            "Ничего интересного, кроме её ножек, открытых полностью."
            "Ножек."
            "Я не понял, в какой момент неотрывно начал фокусироваться именно на них."
            "В сознание постучалась подсознание."
            play music music_7dl["ritual2"] fadein 3
            dreamgirl "Я не знаю, что она делает, но останови её немедленно!"
            th "Что, зачем?"
            dreamgirl "Бегом!"
            "Я попытался было двинуться с места, но не смог."
            dreamgirl "Поздно!"
            dreamgirl "Она что-то сделала с тобой, и я не знаю, что."
            th "О чём ты?"
            dreamgirl "В тебе что-то изменилось, ты не чувствуешь?"
            th "Нет."
            dreamgirl "Прислушайся внимательно. {w}Постарайся понять и почувствовать себя самое. {w}Где что-то не так?"
            "Я растерянно смолк, пытаясь не то двинуться с места, не то понять, о чём говорит мне интуиция."
            scene
            $ renpy.show("bg ext_polyana_night", what = Noir("bg ext_polyana_night", brightness = -.3, saturation = -.25))
            "А Славя повысила голос так, что тот зазвенел."
            sl "Впивайся тоска, въедайся в сердце, в грудь, в живот Симеона-симеона-сына, разойдись, разрастись по всем жилам."
            scene
            $ renpy.show("bg ext_polyana_night", what = Noir("bg ext_polyana_night", brightness = -.4, saturation = -.35))
            dreamgirl "Опять!"
            "Внутренний голос звучал всё слабее, и я всё хуже его слышал."
            scene
            $ renpy.show("bg ext_polyana_night", what = Noir("bg ext_polyana_night"))
            with dissolve
            dreamgirl "Я с трудом держусь, но будет лучше, если ты устранишь того, кто лезет тебе в голову."
            dreamgirl "Не дай ей закончить то, что она начала."
            "Я бы и рад… Но меня парализовало."
            sl "Разойтись костями, сухотой и ноетой по Славяны-послушницы, и не отпускай ни в какой час, ни в злости, ни радости."
            sl "Да будет так!"
            play sound sfx_thunder_crack
            scene white with flash
            "С абсолютно сухого неба грохануло молнией."
            "И я ослеп."
            stop music fadeout 3
            play music music_7dl["dropit"] fadein 3
            scene
            $ renpy.show("bg ext_polyana_night", what = Noir("bg ext_polyana_night"))
            "А когда пришёл в себя, то понял, что если прямо здесь и прямо сейчас не возьму её, стоящую там же, смотрящую прямо на меня сквозь еловые лапы…"
            "То разорвусь — там, внизу, куда, кажется, собралась вся энергия тела."
            show sl normal dress with dissolve
            "На трясущихся ногах я сделал шаг вперёд."
            show sl normal dress close with dissolve
            "Чувствуя, как эго плавится в сладостном вожделении, в предвкушении девичьего тела, не обязательно покорного, но обязательно сладкого."
            show sl normal dress close at center:
                xalign .5 yalign .5
                linear .5 zoom 1.1
            "Теряя себя."
            with fade
            me "Что ты сделала со мной?"
            "Хрипло спросил я, делая шаг ей навстречу."
            show sl smile dress close at center with dspr:
                xalign .5 zoom 1.1
                linear .5 zoom 1.2
            sl "Ты слишком долго ненавидел меня."
            "Улыбнулась она."
            "Поэтому тебе сейчас так не по себе."
            sl "Расслабься. {w}Пусти меня."
            show sl shy dress close at center with dspr:
                xalign .5 zoom 1.2
                linear .5 zoom 1.4
            sl "А я… Пущу тебя."
            "Она покраснела и закусила губу."
            sl "Только не сделай мне больно."
            "Прошептала она."
            if persistent.hentai_graphics_7dl:
                show sl opaq_dress at center:
                    xalign .5 zoom 1.4
                with dissolve
                pause(1)
                show sl normal dress close at center:
                    xalign .5 zoom 1.4
                with dissolve
            "На секунду показалось, что рубашки на ней нет."
            "Её губы были сладкими, её запах сводил с ума."
            "Я протянул руку к ней, уже весь находясь там — в том моменте, когда мы оба окажемся на земле."
            "Она будет неопытной, я буду неуклюжим, нам обоим больше неловко будет, чем приятно."
            "А ей ещё и больно."
            "И уснём в гнезде из разорванной формы и её рубахи."
            "Я — опустошённый, она — заполненная."
            dreamgirl "Нет!" with vpunch
            scene white
            with flash_red
            with diam
            "В голове лопнуло что-то алое, разлапистое, скрутившее всю тело электрической болью."
            "Я почувствовал на верхней губе что-то влажное."
            "И промакнул пальцами — какая-то жидкость. Кровь?.."
            "Кровь."
            scene
            $ renpy.show("bg ext_polyana_sunset", what = Noir("bg ext_polyana_sunset"))
            show sl surprise dress close at center
            with fade
            "Славя выглядела не просто удивлённой, а шокированной — как будто ты уже положил голову на колоду, а у палача вдруг сломался топор."
            scene black with dissolve
            "А потом чёрная пелена застлала мне глаза и я потерял сознание."
            stop music fadeout 3
            $ alt_day5_sl_wh_transit = True
            return

label alt_day5_sl_wh_begin:
    scene cg d5_rainy_idle_7dl with dissolve
    play music music_7dl["ambience_safe"] fadein 3
    play sound_loop ambience_7dl["rain"]
    "Я проснулся от холода и сырости, залезающей, казалось, за шиворот."
    th "Где я?"
    "Я рывком сел и огляделся."
    "Если память мне не изменяет, я был на костровой поляне."
    "Я и сейчас тут лежал, используя одно из бревен в качестве подушки."
    "Начавшийся дождь разбудил меня, а пока я тут головой мотал, успел промокнуть до нитки."
    with fade

    "Вчера произошло что-то очень плохое."
    "Очень неправильное."
    "Не просто же так мне пусто на душе?"
    th "Почему мне…"
    th "Ты там?"
    "Молчание."
    th "Эй, моё похотливое альтер-эго, пора вставать, все встали, и ты тоже."
    "Молчание."
    th "Ау?"
    th "Ты… Ты здесь?.."
    "В том месте внутри меня, где последние несколько дней обитал мой язвительный и циничный собеседник, зияла сосущая пустота."
    th "…"
    "Воспоминания хлынули чередой."
    scene
    $ renpy.show("bg ext_clubs_night", what = Noir("bg ext_clubs_night"))
    show prologue_dream
    with fade

    "Попытки наладить вещание."
    if alt_day4_sl_cl_tut_iz:
        scene
        $ renpy.show("bg int_attic2_day_7dl", what = Noir("bg int_attic2_day_7dl"))
        show prologue_dream
        with fade

        "Звуковая завеса."
    scene
    $ renpy.show("bg ext_old_building_night", what = Noir("bg ext_old_building_night"))
    show prologue_dream
    with fade

    "Попытки найти Шурика."
    scene
    $ renpy.show("bg ext_polyana_night", what = Noir("bg ext_polyana_night"))
    show sl happy pioneer at center
    show prologue_dream
    with fade

    "И вчерашнее непонятное, страшное, {i}чёрное{/i}"
    scene bg ext_polyana_night
    show prologue_dream
    "Но миг я снова почувствовал вчерашнюю безумную похоть, это всесокрушающее чувство желания."
    "Но сегодня я уже мог справиться с этим сам."
    "Тем более, что мой внутренний собеседник всё-таки как-то смог помочь, чему-то научить меня."
    "Пусть и ценой собственного существования."
    scene bg ext_polyana_sunset
    show wet1
    with fade
    "Я с кряхтеньем поднялся на ноги."
    "И, прикинув направление, зашагал в сторону лагеря."
    "У меня было несколько неприятных вопросов к одной ведьмочке."
    stop music fadeout 3
    stop ambience fadeout 6
    with fade
    return