label alt_day4_sl_7dl_old_phone:
    scene bg ext_backroad_day_7dl
    with dissolve
    with vpunch
    "Я не успел додумать о третьей неизменной составляющей моего счастья одиночки, так как со всего маху стукнулся обо что-то ногой."
    play music music_7dl["nap_one"] fadein 5
    "Что-то твёрдое, угловатое, торчащее из травы едва заметно."
    "Очень похожее на…"
    me "Эт-то что за подлодка в степях Украины?.."
    "Присев на корточки, я развёл в стороны заросли лисьего хвоста и кашки и в густой тени разглядел неуместную здесь бетонную плиту."
    "Серая, прямоугольная, такими мостили дорожки и в «Совёнке» по принципу «всё равно тяжёлая, никто не сдвинет», просто кидая на землю."
    "Бетон вообще долгоиграющий, если заботиться о нём, плитки в лагере выглядели чуть вытертыми, но относительно новыми, так что едва ли им было больше десяти-пятнадцати лет."
    "А это же… выщербленная, с высыпавшимися краями, из-за чего оригинальный прямоугольник больше походил на песочные часы, с выглядывающей на месте скола железкой в качестве арматуры."
    th "Едва ли кто-то просто шёл по лесу и бросил её здесь."
    "Как оказалось, тропинка, по которой я шёл до последнего времени, находилась в точности между двумя плитками — видимо, ту, что лежала здесь на земле раньше, то ли унесли куда-то, то ли разбили окончательно."
    "Вдаль в обе стороны от меня уходили две некогда мощёные и напрочь заброшенные дорожки, одна из которых вела определённо в сторону лагеря, а вот вторая…"
    "От моего места тяжело было рассмотреть, куда именно она уходила, так как некрупный холмик, который огибала эта дорожка, перекрывал возможность рассмотреть, куда же там дальше."
    me "Любопытно."
    "Я потрогал плитку ногой, держала та крепко."
    "И, не раздумывая больше, зашагал по дорожке, направился в неизвестность, следя лишь за тем, куда ставлю ногу."
    "Когда я обогнул холм, ощущение лагеря почему-то пропало безвозвратно."
    "Впрочем, в сложившейся ситуации это не особо беспокоило — в крайнем случае, всегда можно будет вернуться по собственным следам обратно."
    "Времени у меня внезапно оказался целый вагон, а заниматься всё равно было нечем."
    "И так оно, наверное, даже и лучше всего — вместо того, чтобы шататься по лагерю, бесить окружающих и высочайшую комиссию, я тихо-мирно удалюсь на свой собственный дальний поиск."
    "Выбирая между бесцельной ходьбой непонятно куда и «добровольной помощью», я всегда выбираю помощь!"
    "Шутка."
    "В одном я был уверен — здесь бояться нечего."
    "Можно ходить сюда сколько угодно, не боясь встретить какую-нибудь опасную для здоровья пакость."
    "Запреты на самовольный уход за территорию носили скорее дисциплинарное значение, чем действительную заботу и боязнь, что с кем-то что-то случится."
    "В конце концов, будь здесь какая-то опасность, не стали бы делать детский лагерь."
    "Само по себе заведение достаточно шумное, способное распугать всё зверьё в округе, оно было к тому же огорожено кирпичным забором."
    "Единственное, кого здесь совершенно логично следовало бы ожидать — это всякую кровососущую пакость вроде комаров или слепней и, разумеется, ёжиков."
    "Последние очень любят столоваться рядом с человеком, подъедая за ним всякие вкусности вроде йогуртов или мороженого."
    "Вот их-то как раз и следовало бы ожидать, а значит — никаких змей, лягушек и прочих земноводных-пресмыкающихся, почитаемых колючками за неплохую закусь."
    scene bg ext_path_day
    with dissolve
    "Успокоив себя и подающий голос инстинкт самосохранения, я беззаботно двигался по тропинке, напевая о том, что «привет, это я»."
    "Пока моему взгляду не открылось то, чего я никак увидеть не ожидал."
    play ambience ambience_old_camp_outside fadein 6
    "Окружённый покосившимся забором одноэтажный дом с надстройкой из дранки."
    "Почему-то смутно знакомый."
    "На лагерной карте у ворот я ничего подобного не видел."
    "И ни от кого из пионеров ничего подобного не слышал."
    "Хотя и общался-то толком, пожалуй, лишь с липучкой-рубль-штучкой по имени Славя."
    "Вот она явно сказала бы, если бы знала, что в окрестностях есть такой вот заброшенный дом."
    th "Такими темпами скоро окажется, что здесь и какое-нибудь сельцо неподалёку имеется — в пешей доступности."
    dreamgirl "Будь оно так, тебе бы сказали."
    th "Мне и про этот дом ничего не говорили!"
    dreamgirl "А тебе зачем? Таким, как ты, скажи про близлежащие руины — так первой же ночью свалите туда косплеить Индиану Джонса пополам с Лорой Крафт."
    th "Лара Крофт."
    dreamgirl "Каяхренразница? {w}Не сказали, и правильно сделали."
    dreamgirl "Самые обычные развалины, ничего интересного."
    "Теперь это видел и я, со всей отчётливостью."
    "Дом представлял собой кирпичное основание с надстройкой, годной для всяческих нужд вроде просмотра кино на случай дождливой погоды."
    "Так вот, надстройка со стенами из плашек, обмазанных штукатуркой, кое-где покосилась, из-за чего оконные переплёты повылетали, а раздавленное стекло осыпалось вниз осколками."
    "На первом этаже окна были относительно целыми, но серая и охряная штукатурка там и сям облупилась, открывая кирпичную кладку…"
    "Избушка стояла к лесу передом, так что пришлось идти в обход."
    stop music fadeout 5
    pause(1)
    "Так я и сделал."
    scene bg ext_old_building_day_7dl
    with fade2
    play music music_7dl["sammy"] fadein 3
    "И оказался на крупной, окруженной хвойными исполинами поляне."
    "Откуда-то справа веяло водой, и слышался едва различимый плеск — видимо, наша лагерная речка делала здесь крюк."
    "Слева же было некогда плотно вытоптанное пространство, сейчас плотно заросшее высоченной — по пояс — травой, перемежаемое лишь вездесущими дорожками из плит, окружённое хвойными исполинами с крюками, вбитыми прямо в стволы…"
    "Похоже, здесь было некое подобие палаточно-тентового городка в духе конца шестидесятых."
    "Весьма распространённая практика летних лагерей середины двадцатого века, когда строили только основной корпус, а пионеров и вожатых расселяли по палаткам."
    "Довольно неплохо расположенное, должен признать, раз за всё время отсутствия человека здесь ничего не подмыло и не разворотило."
    "Судя по всему, лагерь почему-то закрыли, законсервировали, но присутствие человека не лечится так просто."
    "Разве что крюки не повыдёргивали — союз может позволить себе некоторую расточительность в случае, когда дело доходит до экстремальных ситуаций."
    "Здесь было довольно живописно…"
    "Но в глаза прежде всего бросались детские аттракционы-снаряды, триста лет как устаревшие — карусели, качели-распорки, лесенки-радуги — несть им числа."
    with fade
    "Краска давным-давно облупилась, а проглядывающий там и сям металл был густо покрыт сплошной коркой ржи, кое-где покосившиеся бока вросли в землю, так что нечего было и думать прокатиться."
    scene bg ext_old_building_day_7dl:
        linear 0.4 zoom 1.15
    "Я подошёл ближе – и вздрогнул."
    play sound sfx_carousel_squeak
    "Одна из каруселей, видимо, из тех, что сохранились лучше прочих, поприветствовала меня истошным скрипом на ветру."
    me "Бррр, кошмар какой!"
    "Передёрнул плечами я."
    "Дверь в главном корпусе, видимо, выразила своё согласие, с грохотом провалившись внутрь."
    "Уставилась на меня слепым угрожающим провалом."
    with hpunch
    "И ни следа пребывания человека. Как будто на земле пронёсся тихий шторм, уничтожающий всё разумное на своём пути, оставляя за собой лишь тени и нетронутые плоды цивилизации."
    "Вернее, как нетронутые… Всё, что могли, с собой утащили."
    "Остальное — работа времени. Его неповторимый прикус, легко узнаваемый на брошенных вещах."
    "У главного корпуса были две вспомогательные лесенки — должно быть, так строят во всех лагерях на случай пожара."
    "Но нечего было и думать прикасаться к ним — как и качели с каруселями, витые ажурные железные решётки держались на честном слове и не рассыпались трухой только чудом."
    "Их тоже здорово так погрызло время."
    "И тишина здесь стояла такая… применительно к этому месту, к этому окружению эпитет «гробовая» был крайне уместен."
    scene
    $ renpy.show("bg ext_old_building_day_7dl", what = Dawn("bg ext_old_building_day_7dl"))
    with diam
    "Выражаясь языком классика, царила вокруг необъяснимая жуть."
    "Здесь следопытом быть не надо, чтобы понять: произошло здесь что-то этакое, и произошло тогда, когда ни меня, ни того, чьё тело я занимаю, возможно, ещё и на свете-то не было."
    dreamgirl "Или был уже, но в самом нежном возрасте."
    "Когда здесь всё консервировали, по возможности постарались прибраться, однако время, время…"
    "Руины, образовавшиеся естественным путём, как это всегда и бывает без пригляда человека."
    "Казалось бы, живут люди в одном доме десять, двадцать, пятьдесят лет — а он стоит."
    "А тут полтора десятка лет миновало — и всё. Запустение."
    with fade
    "И дух над поляной царил… тягостный, пачкучий, будто произошло здесь нечто неприятное."
    "Но что? {w}Боюсь, некому уже ответить."
    th "Ну что, сыграем в героя второсортного ужастика?"
    dreamgirl "Боюсь, у тебя не получится. Так как маньяки, какие бы здесь ни водились, давно уже должны были передохнуть с голоду."
    th "Вот и мне так думается."
    "Подбодрив себя таким образом, игнорируя надсадно скрипящую карусель, зашагал к чёрному провалу."
    th "Стёкла повылетали, но рамы почти все на месте, подновить штукатурку, подкрасить, застеклить — и можно заселять детишек."
    th "Я никогда не пойму причин, по которым люди бросают плоды своего тяжёлого труда."
    th "Этот дом кто-то же строил, трудился…"
    dreamgirl "А вот сейчас причины и узнаем. Ходу!"
    "Пока я приближался к зданию, моё внимание привлёк какой-то яркий клочок бумаги, изнутри наклеенный на сохранившееся стекло окна."
    "Подобравшись поближе, я прочитал."
    "{b}С С С Р{/b}"
    "{b}МИНИСТЕРСТВО ПРОСВЕЩЕНИЯ РСФСР{/b}"
    "{b}П.Л. «СОВЁНОК-1».{/b}"
    with fade
    "Буквы были выведены трафаретом синей ручкой и сквозь даже пыльное стекло читались замечательно."
    "А вот окно рядом было зачем-то наглухо заколочено снаружи. Хотя сквозь щели видно было, что стекло всё ещё на месте, но из-за разницы в освещении ничего внутри разглядеть не удалось."
    "Так или иначе, шататься вокруг да около долго ещё можно было."
    "И уезжали отсюда отнюдь не просто так, не от нечего делать."
    "Меня раздирало любопытство пополам со страхом."
    with fade
    "Вздохнув несколько раз, я переборол страх и направился ко входу в здание."
    "Как оказалось, дверь была не одна — та, что падением поприветствовала меня, лежала, упираясь краем в полураспахнутую вторую."
    "Из-за получившейся инсталляции проникнуть в здание было затруднительно."
    "Пришлось потратить с десяток минут, раскачивая сооружение из стороны в сторону."
    play sound sfx_open_door_squeak_2
    pause(1)
    scene
    $ renpy.show("bg int_old_building_day_7dl", what = Notch("bg int_old_building_day_7dl"))
    show blackout2
    with dissolve
    play ambience ambience_7dl["int_silence"] fadein 3
    "В итоге я победил."
    "Баррикада рассыпалась с душевынимающим визгом, сделавшим бы честь иному голливудскому ужастику, заставив сердце подпрыгнуть и заколотиться где-то в районе горла."
    "Я чуть было не отскочил назад, решив плюнуть на происходящее, но раз уж взялся за гуж…"
    "Твердя одну и ту же мантру, я переступил через порог и сморщился от шибанувшего в нос запаха пыли и запустения."
    play sound sfx_gusty_wind
    "Справа от меня в разбитое стекло задул ветер и завыл между рамами, и я снова забормотал про себя:"
    th "Здесь не опасно, здесь всё хорошо!"
    th "Если бы здесь было что-то нехорошее, меня обязательно предупредили бы, заставили бы выслушать инструктаж и настрого запретили приближаться сюда."
    "Лагерное начальство обвинить можно было в чём угодно, но не в безалаберном отношении к пионерам."
    "В конце концов, сегодняшняя сутолока прямо отрицает любые подобные обвинения."
    scene bg int_old_building_day_7dl
    show blackout2
    with fade
    "Сначала я обнаружил себя в крохотном коридорчике, скорее даже сенях, ведущих сразу в пяти направлениях."
    "Отсюда можно было отправиться обратно на свежий воздух, вверх или вниз по находящейся непосредственно передо мной лестнице, вправо или влево по расходящимся в разные стороны коридорам."
    "Лестница была сделана из деревянного бруса, выкрашенного некогда коричневой краской, и даже с виду внушала серьёзные опасения в своей прочности."
    "Это же подтверждали и полуосыпавшиеся штукатурные стены этой самой лестницы."
    "Путь направо был усыпан битым кирпичом, а в полу зияла солидная такая дырища, сквозь которую можно было увидеть какую-то металлическую сетку, растянутую параллельно полу."
    "Налево же был деревянный барьер высотой по пояс с откидной крышкой, на котором легко можно было разглядеть угрожающе-красную надпись:"
    me "«Администрация, посторонним вход воспрещён!»"
    "Я провёл пальцем по откинутой крышке — слой пыли был чуть ли не в полсантиметра толщиной, за барьером тоже царила разруха и запустение, однако хотя бы возможно было пройти."
    th "Значит, нам туда дорога."
    "Не решаясь трогать здесь ничего без лишней необходимости — а ну как здесь стена держится только за счёт этой самой крышки? — я поднырнул под крышку и ползком прополз на запретную часть."
    "Посторонний-то я был, посторонний, но, полагаю, некому спросить с меня за нарушение правил."
    "Лет пятнадцать как некому."
    "Прямо передо мной была тяжёлая дверь из цельного дерева с табличкой «Румпель М.М., начальник лагеря», выкрашенная в пару десятков слоёв дешёвой коричневой краской, а направо был некий коридорчик."
    "Снаружи там можно было рассмотреть с пяток светлых квадратов на стенах, судя по всему, оставшихся после каких-то снятых досок или фотографий, а чуть дальше проход перекрывала вывороченная дверь с красным крестом."
    "Медпункт, очевидно."
    with fade2
    "А вот чуть подальше висела доска, явно забытая, на которой висело несколько фотографий!"
    "Я подошёл поближе, присмотрелся."
    "К сожалению, из-за времени все фотографии пожелтели, свернулись и утратили контрастность настолько, что разглядеть можно было только некоторые лица, а большинство и различалось-то с трудом."
    "Буквы, нанесённые какой-то краской вроде гуаши, тоже давным-давно осыпались, так что надпись можно было прочитать лишь по более тёмному остаточному следу."
    me "Наша гордость."
    "Прочитал я."
    me "Третья смена 1974 года."
    "Старшая вожатая Баранова Т.С.; медсестра Трофимова А.Ч.; плаврук Денисенко С.Ю.; замнач Трофимов А.М."
    "Эти фотографии были крупнее, потому и сохранились чуть лучше, и однофамильцев медсестру и заместителя можно было худо-бедно разглядеть."
    "На Шурика они совершенно не походили."
    "Нижнему ряду, фотографиям 9х12, повезло значительно меньше, и здесь разглядеть лица не представлялось возможным."
    "Хотя некоторые фамилии и будили любопытство."
    "Например, некая Сидорова Алиса, первое место по лёгкой атлетике среди девочек 12-14 лет."
    "Потому как знал я одну Сидорову. Правда, она совсем не Алиса была."
    with fade2
    "Или вот, в чёрном уголке с молнией и надписью «ПОЗОР»."
    "Доска была куда старее, чем доска смены — видимо, её однажды повесили сюда и с тех пор забыли."
    "Так что даже окажись там фотографии, всё равно невозможно было бы что-то различить."
    "Но снимки, пришпиленные на кнопки, были кем-то безжалостно содраны, так что остались только клочки по углам."
    "И надписи выцвели до почти нечитабельных, различить удавалось только часть."
    "Говоров Архип, за саб… р…ты ра…нции."
    "Вторую фамилию прочитать не удавалось. Какая-то, заканчивающаяся на «и» краткое. И имя специфическое, Викентий. «За на…ние зап… …вотных …ере»."
    "Незнакомые люди, незнакомые лица."
    "По идее, они должны были оставить меня равнодушным, но вместо этого почему-то взволновали."
    th "Наверное, они уже и забыли о том, как когда-то заслужили внимание целого лагеря…"
    "Вздохнув, я отошёл от стенда и вернулся к двери в кабинет начальника."
    "Она долго не хотела поддаваться — видимо, от сырости и перепадов температуры покосилась дверная коробка, потому дверь и переклинило."
    "Пришлось чуть поднимать и тянуть и на себя все эти килограммы."
    play sound sfx_open_door_squeak_2
    "Однако удача улыбается терпеливым — десять минут спустя я проник в святая святых."
    "И замер…"
    scene bg int_old_building_cab_day_7dl with fade2
    "Кабинет начальника лагеря окнами смотрел на сам лагерь, позволяя вседержцу быть в курсе всего происходящего."
    "Помещение перегораживал огромный деревянный стол, а вдоль стены по периметру выстроились шкафы, удивительно похожие на те, что стоят сейчас в кабинете Алексей Максимовича."
    dreamgirl "Вот будет забавно, если у него фамилия Трофимов, правда?"
    th "В смысле?"
    "Уточнил я, озираясь."
    "Дальнюю часть стола засыпало обвалившимся шкафом и его содержимым, в числе которых были и обтянутые грубой тканью папки — то ли дела, то ли вопросники."
    dreamgirl "Здесь заместителем трудился некто Трофимов А.М. Как считаешь, совпадение?"
    th "Я не думаю, что…"
    stop music
    stop ambience
    play music music_7dl["dead_silence"] fadein 3
    play sound_loop sfx_7dl["old_phone"]
    with flash
    with vpunch
    play ambience ambience_7dl["int_silence"] fadein 5
    "Я не успел договорить, так как в мёртвую тишину этого места, вдребезги разбивая вернувшееся было душевное равновесие, вплёлся неуместный здесь звук!"
    "Звонящий телефон!"
    "Меня всего съёжило от страха, пробило ледяными мурашками между лопаток, а в голову сразу прыгнули кадры из «звонка» и Дохлой Девочки, лезущей из телевизора."
    "Звон доносился откуда-то близко, как будто телефон стоял прямо здесь передо мной на столе, однако в действительности на девственно-пустом пространстве столешницы не было даже бумажек."
    "Исключая, понятно, заваленный папками угол."
    scene cg d4_sl_phone_ring_7dl with vpunch
    with flash
    "И я на ватных ногах отправился туда."
    "Сердце снова колотилось пойманной птицей, виски ломило от напряжения, когда я, ориентируясь единственно на слух, протянул руку и толкнул в сторону ворох папок."
    "Те с едва слышимым шуршанием сползли в сторону, оставляя на виду старый-старый дисковый агрегат с кожухом из чёрно-зелёной пластмассы."
    "Именно он и надрывался, разрывая тишину на миллион осколков."
    th "Кто бы это мог быть?"
    "Задумчиво посмотрел я на телефон, вовсе не спеша снять трубку."
    th "Вряд ли сюда ведут какие-то коммуникации."
    menu:
        "Снять трубку":
            stop sound_loop
            $ alt_day4_sl_7dl_phone = True
            scene cg d4_sl_phone_up_7dl
            with flash
            "Я протянул руку и снял пыльную тяжёлую трубку, приложил к уху."
            me "Слушаю."
            "С пару секунд там царило молчание, а потом мне в барабанную перепонку вонзился визгливый, панический женский голос."
            voice "Говорит Мария Трофимова!"
            "Визг ввинтился в ухо, заставляя многострадальную голову гудеть, и я поспешно убрал мембрану подальше от уха."
            voice "Код «красный», проникновение на территорию объекта! {w}Свяжитесь с Коломийцевым."
            "Женский голос умолк на несколько мгновений, в трубке все это время слышались непонятные шумы и трески."
            voice "Алиска и Анна умудрились просочиться в кабинет, а оттуда и в коридоры, несмотря на охрану!"
            voice "Сидорова понятно за чем побежала, а у сестры никогда не хватало мозгов!"
            voice "Они обе стёртые, слышите? Обе! Нужна спецбригада из центра реабилитации."
            "Голос опять замолчал, в трубе осталось лишь прерывистое дыхание и всхлипы."
            voice "Связь сдохла вся, ни радио, ни телефоном до города больше не достучишься! Если это будешь ты, Вилка, храни бог твою душу!"
            with flash
            "В голосе было столько отчаяния и безнадёжности, что я не выдержал и крикнул в трубку."
            me "Это не Вилка, это Семён! Где вы?"
            "Не выдержал я."
            me "Кто такой Коломийцев?!"
            "Я вдруг заметил одну странность."
            "От телефона не уходила никуда привычная витая пара. По сути, это был просто автономный аппарат без возможности связи с внешним миром."
            "Неизвестная Мария, проплакавшись, снова завела своё:"
            voice "Говорит Мария Трофимова! Да слышит меня кто-нибудь?!"
            voice "У меня двое стёртых на руках! А начальник делся неизвестно куда. Я вообще сюда на родительский день приехала, к сестре! Пожалуйста… Я не знаю, что делать."
            "Похоже, это был далеко не первый и даже не десятый раз, когда Мария говорила это в трубку — по ту сторону мембраны хватало и безнадёжного отчаяния, и слепого нежелания сдаться."
            "И Семён… {w}Сычёв Семён, никогда не любивший людей и не пытавшийся этим людям помочь, с удивившей себя злостью заорал в ответ:"
            me "Да слышу я вас! Слышу! Меня зовут Семён, я из соседнего лагеря, тоже «Совёнка!»"
            me "Где вы?!"
            "В трубке что-то заскрипело, вздохнуло, а потом одновременно на заднем плане что-то упало, загрохотало, а на переднем омерзительно хрустнуло."
            "Донёсся истошный сип."
            "И всё стихло."
            scene cg d4_sl_phone_ring_7dl with vpunch
            with fade
            "Подождав ещё несколько минут, я как мог аккуратно вернул трубку на рычажки и отступил от телефона на шаг назад."
            me "Здесь произошло что-то на редкость нехорошее."
            "Подал голос я."
            dreamgirl "Ты про хруст? Обычно так ломается шея."
            dreamgirl "Как в фильмах, знаешь? Мёртвый захват, рывок подбородка, всё такое."
            dreamgirl "Или когда человек вешается на тонком, прочном шнуре."
            th "Валить надо отсюда. Зря я пришёл."
            "Я развернулся, чтобы уйти…"
            play sound_loop sfx_7dl["old_phone"]
            "И в этот момент телефон снова заорал, так же требовательно, надрывно, выматывающе звонко."
            scene cg d4_sl_phone_up_7dl
            with dissolve
            stop sound_loop
            "Кончиками пальцев, как дохлую крысу, я подобрал трубку с рычажков."
            "Поднёс к уху."
            show blackout2
            with fade
            voice "Говорит Мария Трофимова!"
            "Снова заплакал в трубке женский голос."
            voice "Код «красный», проникновение на территорию объекта!"
            "Она повторяла всё те же слова, раз за разом, в точности, будто это была запись или что-то в этом роде."
            "А я терпеливо слушал и ждал, в этот раз уже не пытаясь докричаться до женщины."
            "Похоже, до неё тоже уже очень-очень давно не докричишься."
            "Выслушал всё."
            "Включая упавшую табуретку и хрипы сдавливаемого горла."
            "А выслушав, положил трубку на место."
            scene cg d4_sl_phone_ring_7dl
            with fade
            "Телефон зазвонил и ещё раз, но в этот раз я и не думал подходить."
            "Больше чем уверен был в том, что придётся слушать всё то же самое ещё раз."
            "А мне, честно, хватило первых двух."
            "Вместо этого я поднял несколько папок и обложил ими аппарат со всех сторон, накрыл сверху."
            "И только после этого телефон, будто успокоившись, звонить перестал."
            "Лязгнул трубкой и смолк."
            "Понятия не имею, мистика, фантастика или просто запись с датчиком движения?"
        "Не снимать":
            $ alt_day4_sl_7dl_phone = False
            "Телефон звонил ещё с полминуты, прежде чем смолк, и не было в мире силы, способной заставить меня прикоснуться к запыленной трубке."
            "Понятия не имею, мистика, фантастика или просто датчик движения?"
    stop sound_loop
    scene bg int_old_building_cab_day_7dl with dissolve
    th "Хотя какие, нафиг, датчики в лагере семидесятых?"
    "Однако складывалось впечатление, будто я одним лишь своим присутствием пробудил дремлющее здесь старое зло, вернул к жизни призраков былого, не то оригинальных, с цепями, не то фантомные обрывки в электронных потрохах."
    "Вот теперь и в самом деле проявилось то самое чувство места, откуда лучше бежать без оглядки."
    "В призраков людей я не верил, но как насчёт призраков идей?"
    if alt_day4_sl_7dl_phone:
        "Или призраков телефонного разговора в отключенном аппарате?"
    th "Валим."
    dreamgirl "Поддерживаю."
    scene bg int_old_building_day_7dl with fade
    play sound sfx_slam_door_campus
    "Я вышел в коридор административной половины и захлопнул за собой дверь."
    "Мне и думать не хотелось, что тут могло бы ещё вернуться к состоянию бытия, уловив наличие дышащего, мыслящего, а главное — живого — человека, после стольких лет забытья."
    "И не суть важно, мнительный я или скептик — здесь определённо {i}что-то было{/i}."
    "Что-то, с чем я очень не хотел бы встречаться."
    th "«Лагерь — место безопасное», — говорили они."
    th "«Мы вам обо всём расскажем», — говорили они."
    "Щекочущий спину холодок придавал азарту похлеще пресловутого волшебного пенделя, так что я буквально вылетел в коридор, рыбкой нырнул под полку и, пинком распахнув дверь"
    stop music fadeout 6
    pause(1)
    play sound sfx_open_door_kick
    pause(1)
    scene
    $ renpy.show("bg ext_old_building_day_7dl", what = Noon("bg ext_old_building_day_7dl"))
    with flash
    extend ", вывалился наружу!"
    play ambience ambience_old_camp_outside fadein 3
    "Здесь было уныло, запустело и затхло, но хотя бы не так, как там, внутри."
    "Я никогда не страдал клаустрофобией, а здесь стены сами будто давили на меня, заставляя прибавлять ходу."
    "Посмотрел на остатки каруселей, на радуги-лесенки уже совсем другим взглядом."
    if alt_day4_sl_7dl_phone:
        "Здесь кто-то погиб, а с двумя другими произошло что-то на редкость гадкое."
        "Я не мог знать, что может значить «стёртый», но предполагал, что просто так интонацией это слово не выделяют."
    "Однако то, насколько тщательно здесь выгребли всё, выдаёт то, что никто особо не торопился."
    "Отсюда вывезли всё, что сочли нужным — с местным-то богатым государством — и вывезли, чётко осознавая все рамки и ограничения."
    play sound sfx_open_door_squeak_2
    "Дверь за спиной снова издевательски заскрипела, вынуждая подпрыгнуть на месте."
    me "Тьфу!"
    th "К дьяволу местные тайны."
    "Стараясь избавиться от впечатления пристального взгляда в спину, я заспешил туда, откуда доносился плеск воды."
    th "Надеюсь, там никто не утопился…"
    scene bg ext_beach2_day_7dl
    with dissolve2
    play ambience ambience_lake_shore_day fadein 6
    play music music_7dl["unfulfilled"] fadein 3
    "И лишь выскочив на берег, почувствовал, как пропало неотпускаемое чувство взгляда в спину."
    "Да, я струсил. А кто бы нет?"
    "Задним числом я вообще не мог понять, какого чёрта меня потянуло в эти развалины."
    "Как ни крути, но никогда я не был в нужной мере сумасшедшим, чтобы лезть непонятно куда непонятно зачем."
    "А здесь будто кто-то под руку толкнул."
    "Кто-то, кому я с удовольствием съездил бы пару раз по челюсти."
    dreamgirl "Эт не я!"
    th "Ну разумеется."
    "Я уселся на крайне удачно брошенную на песке корягу и прикрыл глаза."
    show blinking
    stop music fadeout 3
    stop ambience fadeout 6
    pause(2)
    if not persistent.pivo_default_7dl:
        jump alt_stories_start
    else:
        return