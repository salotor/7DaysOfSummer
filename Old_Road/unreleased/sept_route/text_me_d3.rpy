label alt_day1_me_d3_M:
    scene anim_underwater
    show blackout_exh
    with fade
    play ambience ambience_7dl["underwater"] fadein 3
    "Воды в трёхлитровой банке хватило, чтобы утопить меня с головой."
    "Связать руки и тело, выдавить из лёгких воздух…"
    "Но сейчас иначе нельзя. {w}Я не помню, кто и когда сбегал таким образом от смерти, но это было."
    "Я точно знал."
    "Как безошибочно знают дети об ожидающих дома неприятностях."
    show blinking
    "Чёрная, мутная вода отпускала меня неохотно."
    "Аллегорическая картина моей жизни."
    "Беспросветное, гнилое болото."
    "Ничего хорошего в этом нет, но… Так живут миллионы людей."
    "А я всю жизнь считал благом способность не выделяться из толпы."
    "Поэтому я замер между двумя разрывающими меня векторами."
    "Между затягивающей меня трясиной и новой силой, тянущей вверх."
    hide blackout_exh
    show blackout_exh2
    with dspr
    "В идеальном балансе в пустоте."
    play sound sfx_head_heartbeat
    "Я остался бы здесь навсегда."
    hide blackout_exh2
    show blackout_exh3
    with dspr
    "Но мне очень хочется дышать."
    "Ещё один гребок к поверхности."
    "Ещё один."
    play sound sfx_bus_window_hit
    with vpunch
    scene black with flash_red
    stop music fadeout 2
    "Бам!"
    pause(1)
    scene bg int_aidpost_day
    with dissolve
    play ambience ambience_medstation_inside_day fadein 3
    play music music_list["so_good_to_be_careless"] fadein 5
    "Кажется, я слишком резко вскочил — от столкновения с чем-то из глаз посыпались искры."
    $ meet('mi', 'Голос')
    mi "Ай!"
    "Это было действительно больно."
    "Несколько секунд я пытался справиться с невольными слезами, и не сразу обратил внимание на то, что я не один!"
    "Мало того, я был в какой-то комнате и не помнил, как я сюда попал."
    "Последнее, что задержалось в памяти — это отражение света на потолке."
    "А потом… Что было потом?"
    "Это было какое-то медицинское помещение — не то санчасть, не то поликлиника."
    "В воздухе витал устойчивый запах лекарств."
    "Сквозь приоткрытую форточку задувал лёгкий летний ветерок."
    "Что-то в сложившейся картине показалось странным, но…"
    mi "Ой, вы живой? Как здорово, а то я тут думала, что вы умерли, лежите такой не шевелитесь, не дышите, знаете как напугалась!"
    "Источник шума удалось локализировать."
    play music music_7dl["ourfirstmet"] fadein 5
    scene
    $ renpy.show("bg int_aidpost_day", at_list = [zenterleft], what = Noon("bg int_aidpost_day"))
    show mi smile pioneer at cleft
    with dissolve
    show mi grin pioneer at cleft with dspr
    show name_mi with flash:
        pos(1160,460)
    show mi happy pioneer with dspr
    pause(2)
    scene bg int_aidpost_day
    show mi shocked pioneer at cright
    with dissolve
    $ meet('mi', 'Японка')
    "Снова внутри будто с ледяным хрустом лопнуло нечто невероятно голубое и воздушное."
    "Захотелось засмеяться или заплакать — что угодно."
    "Ведь я знал, знал эту девушку."
    "Помнил эти бесконечные хвосты аквамариновых волос, этот чуточку рассеянный, тёплый взгляд."
    "Но откуда?.."
    mi "А я же не знаю, как вам помочь, вдруг вы совсем-совсем померли, а я даже и не знаю, а когда вы дышать перестали, я уже думала за доктором бежать!"
    "Та-{w=.2}ра-{w=.2}тор-{w=.2}ка."
    "Голос высокий, но приятный, поставленный. {w}Хотя и чувствуется, что язык ей не совсем родной, да и букву «л» она выговаривает неохотно."
    "Всё это я помнил с тех самых пор, как…"
    scene bg ext_gostinka_night_7dl
    show alt_credits "Ты потерялся, малыш?":
        pos (1227,105)
    pause(.2)
    scene bg int_aidpost_day
    show mi shocked pioneer at cright
    "Девушка выглядела озабоченной, будто изо всех сил пыталась скрыть смущение."
    "Она смотрела на меня, пока я был в отключке?"
    am "Где… Я?"
    "Я не узнал собственный голос — этот был почему-то очень высоким."
    "Мальчишеским."
    show mi normal pioneer with dspr
    mi "Ой, слава богу, вы в порядке, а то я уже было заволновалась совсем!"
    mi "Мы нашли вас у ворот, где вы лежали без сознания, и нам пришлось отнести вас в медпункт, а доктор Виолетта сказала, чтобы мы все от вас отстали, но разве можно отстать, я же волнуюсь!"
    am "Со мной всё в порядке."
    "Я старательно делал вид, что лоб не зашиб, что в голове у меня не творится кавардак и вообще всё просто замечательно."
    "Хотя…"
    scene bg int_aidpost_day:
        xalign .5 yalign .45 zoom 1.0
        easeout 1.0 zoom 1.45 xalign 0.5 yalign 0.75
    pause(0.81)
    play sound2 sfx_bodyfall_1
    with vpunch
    "Я попытался подняться, но вместо этого только успел отметить, как комната почему-то пошатнулась и начала заваливаться вперёд."
    scene bg int_aidpost_day:
        xalign 0.5 yalign 0.75 zoom 1.45
        easein 3.0 zoom 1.05 xalign 0.5 yalign 0.5
    "Меня подхватили тонкие руки, а их хозяйка, успокаивающе говоря мне что-то, вернула меня на кушетку."
    show mi upset pioneer at cright with dspr
    mi "Вы, пожалуйста, не волнуйтесь, доктор Виолетта сказала, что у вас тяжело проходит акклиматизация, поэтому вам надо хотя бы час поспать, иначе будете чувствовать себя плохо и голова будет тяжёлая."
    th "Голова у меня и так будет тяжёлой, если ты не перестанешь трещать."
    "Почему я чувствую себя так, как будто мне по голове как следует стукнули?"
    show mi serious pioneer with dspr
    mi "А раз уж я всё равно помогла вас сюда принести, то и вызвалась добровольцем посидеть рядом, пока она принесёт вам что-нибудь переодеться."
    "Она украдкой потёрла лоб, на котором было заметно ярко выраженное красное пятно."
    "Значит, столкновение с кем-то мне не приснилось?"
    "Но как это возможно?"
    "Она что, подглядывала за тем, как я сплю?"
    dreamgirl "Вот свезло, так свезло. {w}Извращенка!"
    th "Ты снова здесь, чудище?"
    dreamgirl "Missed me?"
    mi "Я говорю…"
    "Опять завела шарманку девушка, а я снова отключился."
    "Что-то ворочалось в голове, заставляя чувствовать себя необъяснимо счастливым. {w}Что-то…"
    "Как будто встретил мечту всей своей жизни."
    show mi happy pioneer with dspr
    mi "…видели кошмар, поэтому я наклонилась посмотреть, что случилось, а вы тут — бац!"
    am "Так. {w}Стоп."
    "Поднял руки я."
    menu:
        "Начнём сначала":
            am "Уот из ёр нэйм?"
            show mi surprise pioneer with dspr
            mi "Что? {w}А, я Мику."
            $ meet('mi', 'Мику')
            "Из бездонных глубин выплыл ещё один пузырь, и я автоматически переспросил:"
            $ reput += 10
            am "Не Намики?"
            show mi shocked pioneer far with hpunch
            "Девушка мгновенно отпрыгнула назад на пару шагов и отчаянно затараторила:"
            mi "Что? Нет, это моё имя Мику, меня просто называют так, потому что очень похожие имена, понимаете?"
            "Затараторила она, отступая ещё на один шажок."
            mi "Сейчас доктор придёт, она вам всё скажет, вы её просто подождите, она у нас лучшая, мигом поможет!"
            "Она выглядела нешуточно напуганной, и я не мог взять в толк — чем."
            am "Ладно."
            "Буркнул я."
            am "Раз доктор приказал…"
            "Я снова улёгся на кушетку, а японка устроилась рядом на скрипучем стуле."
        "Ты подглядывала за мной, пока я спал?":
            show mi shy pioneer with dspr
            mi "Я просто хотела убедиться, что у вас всё хорошо, когда мне в детстве снилось что-то нехорошее, мама всегда дула мне на лоб, и это помогало."
            "Признаться, я впервые слышал про такой способ борьбы с кошмарами."
            "Да и девушка выглядела крайне смущённой."
            th "Хотя ей-то чего стесняться — она меня чуть ли не вдвое моложе."
            "Но… Она за мной подглядывала, а я стукнул ей по лбу."
            "Многообещающее начало знакомства, как по мне."
    am "Стоп, а что значит «переодеться»?"
    mi "Простите?"
    am "«Пока она принесёт вам что-нибудь переодеться»."
    "Старательно процитировал я."
    show mi smile pioneer with dspr
    mi "Какой вы милый."
    mi "Но вы же не собирались ходить на улице в свитере и ботинках?"
    show mi normal pioneer with dspr
    am "А что в этом такого?"
    stop music fadeout 10
    cs "Ровным счётом ничего."
    scene bg int_aidpost_day
    show mi normal pioneer at fright with moveinright
    show cs normal at cleft
    with dissolve
    "В комнате появилось новое действующее лицо."
    play music music_list["eternal_longing"] fadein 5
    cs "Виолетта Церновна Коллайдер."
    $ meet('cs', 'Виолетта')
    show cs grin with dspr
    pause(0.25)
    show cs normal with dspr
    "Она подмигнула."
    cs "Но для тебя можно просто {i}Виола{/i}… пионер."
    $ meet('cs', 'Виола')
    show cs normal close with dissolve
    "Она вежливо кивнула девушке:"
    cs "Твоя работа здесь закончена… пионерка."
    show mi shy pioneer with dspr
    mi "Но я…"
    cs "Но если хочешь помочь…"
    play sound sfx_open_water_sink
    play sound_loop sfx_water_sink_stream fadein 1
    show cs normal with dissolve
    "Виола отошла к раковине в углу помещения и стала неторопливо мыть руки."
    cs "Достань, пожалуйста, из верхнего ящика стола…"
    show mi normal pioneer far at right with dissolve
    "Девушка тут же кинулась выполнять просьбу."
    "Открыла ящик и достала оттуда какой-то серый бумажный пакетик."
    mi "Это, да? Изделие номер два?"
    "Доктор осталась невозмутимой."
    show cs smile with dspr
    cs "Я абсолютно точно уверена, что это изделие номер два."
    cs "Но ты точно уверена, что это… {i}то, что тебе нужно?{/i}"
    show mi shy pioneer far at right with dspr
    "А вот до девушки, кажется, дошло."
    mi "Но это же пре… Го… Э… Ах!"
    play sound sfx_close_door_clubs_nextroom
    hide mi with easeoutright
    "Дверь за девушкой захлопнулась."
    cs "Ой. {w}Не тот ящик."
    "Безмятежно улыбнулась доктор Коллайдер."
    stop sound_loop
    play sound sfx_close_water_sink
    cs "И почему они так пугаются безобидного резинового изделия? {w}Но мы…"
    "Виолетта Церновна подошла к столу и открыв верхний ящик — но по левую руку — достала оттуда комплект одноразовых перчаток."
    cs "Вот о чём."
    show cs normal with dspr
    cs "Займёмся тобой… пионер."
    "Она произнесла это таким тоном, что в памяти почему-то всплыло слово «флюгегехаймен»."
    am "Так я же не пионер."
    "Виолетта Церновна чарующе улыбнулась."
    cs "Уверяю тебя…"
    show cs smile with dspr
    extend " Это временно."
    show cs smile close with dspr
    "Она толкнула меня на кушетку."
    am "А можно я…"
    show cs normal close with dissolve
    cs "Можно…"
    show cs normal close:
        linear 0.5 zoom 1.3
    with dspr
    "Её дыхание ласкало моё ухо."
    cs "…раздеться."
    cs "Или мне самой тебя раздеть?"
    "В её ласковой улыбке я углядел нечто пугающее."
    "Да и никелированные ножницы, которыми она хищно щёлкнула, внесли свою лепту в мой душевный разлад."
    th "Ну её… Кто знает, что у неё там на уме!"
    "Уступив угрозе, я быстро-быстро стащил свитер и футболку, оставшись в одних штанах."
    "Подумывал расстегнуть и их, но Виолетта Церновна меня остановила:"
    hide cs
    show cs normal at fleft
    with dissolve
    cs "Давай не будем спешить… пионер. {w}Мы ещё не так близко знакомы."
    with fade2
    "В голове мелькнуло, что, возможно, докторша просто мается от безделья и развлекается таким образом."
    th "С такими-то методами работы… Неудивительно."
    "Болит-не болит, пульс, лимфатические узлы — в том, что она была профессионалом, сомневаться не приходилось."
    "Но врачи, в руки которых мне приходилось попадать раньше, делали всё быстро, споро, сухо."
    "От доктора же Коллайдер даже простейшее «дышите-не дышите» вызывало мурашки."
    "Меня лапали — и я ни капельки не преувеличиваю!"
    "Всё это было, конечно, здорово и приятно. {w}Но я к такому просто не привык."
    cs "Что сказать…"
    "Закончив осмотр, больше походящий на сексуальное домогательство, доктор стянула перчатки и бросила их в стоящее тут же мусорное ведро."
    show cs smile with dspr
    cs "Нормальный здоровый пионер. {w}Пульс и давление в порядке. {w}Все прочие реакции"
    show cs shy with dspr
    extend " — тоже в полном порядке."
    "Виолетта поднесла мне смутно знакомую розовую плоскую таблетку с поперечной риской и мензурку с водой, и я, не задавая лишних вопросов, выпил лекарство."
    "Иначе бы добрейшая доктор изобрела бы пару альтернативных способов — и в числе гипотетических ощутимее всего фигурировала клизма."
    cs "Насчёт одежды договориться не удалось, придётся тебе обращаться к вожатой."
    show cs normal with dspr
    cs "Вожатой скажешь, чтобы дала тебе пару дней на акклиматизацию."
    cs "И чтобы никаких спорадических… нагрузок."
    with flash2_red
    "Я не знал, что такое «спорадические нагрузки», но на всякий случай покраснел."
    cs "А теперь иди… пионер."
    cs "В кустах рядом с крыльцом тебя дожидается провожатая."
    play sound sfx_hiding_in_bush fadein 0
    dv "Ничего я не дожидаюсь!"
    "Донеслось с улицы."
    cs "Да, прости. {w}Набирается опыта… Анатомического."
    "С улицы донеслось отчётливое рычание."
    cs "Ступай."
    "Она подтолкнула меня к выходу на улицу."
    cs "Не заставляй пионерку ждать."
    stop music fadeout 3
    scene white with flash
    play sound sfx_close_door_clubs_nextroom
    "Меня ослепило не по-зимнему ярким солнцем, бьющим прямо в глаза."
    scene bg ext_sky_7dl with diam
    "До сей поры я берёг психику, не давая себе задумываться…"
    "Но сейчас уже было просто бесполезно отрицать."
    scene bg ext_aidpost_day with flash:
        xalign 0.5 yalign 0.25 zoom 1.25
        linear 0.8 zoom 1.25 xalign 0.5 yalign 0.5
    play ambience ambience_camp_center_day fadein 3
    am "Вашу… Машу…"
    "Мне это не казалось, не"
    with flash_red
    extend " снилось."
    "Я ущипнул себя, но не проснулся."
    "Я заснул в зимнем Петербурге, а проснулся… {b}В ЛЕТЕ{/b}."
    with vpunch
    "Наверное, я сейчас должен был метаться в кошмаре по улице и рвать волосы на голове."
    "Потому что единственный способ перенести человека из зимы в лето — это сменить климатический пояс."
    "Наверное, держали под наркотиками, пока везли в самолёте!"
    th "Зря я эту таблетку сейчас съел."
    th "А то не очнуться бы мне теперь без почки!"
    th "Куда вы меня завезли, гады, сволочи, живым не дамся… Стоп."
    "Я помотал головой."
    th "Почему это звучит так… неубедительно?"
    "Я паниковал по инерции, просто потому, что в такой ситуации полагается паниковать."
    "Все герои-попаданцы, оказавшись в моей ситуации, испытывали сильный стресс, нервное напряжение."
    "Некоторые, особо бойкие, даже начинали кататься по земле и плакать."
    "И я, по идее, тоже должен бы орать и бегать по потолку."
    "Разве нет?"
    "Но медосмотр оказал поразительно релаксирующее действие — я был спокоен как удав."
    th "Или всё дело в таблеточке, подозрительно похожей на алзолам?"
    "Так или иначе… Я огляделся и констатировал очевидное:"
    am "Лето. {w}Без рекламы и СМС."
    "Звоните 911."
    am "Лето."
    "Столбик крылечка рванулся мне навстречу и провёл контрольный удар по недобитой головушке…"
    show blink
    play sound sfx_bus_window_hit
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_A:
    scene black with dissolve
    dv "Ну и долго ещё собираешься изображать припадочного?"
    play music music_7dl["sheiscool"] fadein 3
    "Раздался новый голос."
    "Голос дерзкий, решительный."
    "Я почему-то сразу представил себе его обладательницу рыжей и злой."
    scene
    $ renpy.show("bg ext_aidpost_day", at_list = [zenterleft], what = Noon("bg ext_aidpost_day"))
    show dv smile pioneer2 at cleft
    with dissolve
    show dv laugh pioneer2 at cleft
    show name_dv with flash:
        pos(1220,500)
    show dv normal pioneer2 at cleft
    pause(2)
    scene bg ext_aidpost_day
    show dv angry pioneer2 at cleft
    with dissolve
    "И не ошибся."
    "Она действительно была огненно-рыжей."
    "И очень, очень злой!"
    play sound sfx_punch_medium
    with Shake((0, 0, 0, 0), 0.1, dist=50)
    "Судя по тому хлопку, который пришлось пережить моей несчастной спине, новая вредина вложила в него всю злость за то, что была обнаружена."
    am "Эй, за что?!"
    dv "Заткнись и пошли."
    "И, не слушая моих возражений, взяла меня за руку и поволокла за собой."
    "Сложилось впечатление, что она с большим удовольствием вцепилась бы в ухо."
    "Или поволокла бы меня за ногу, пересчитывая моим затылком все неровности местного ландшафта."
    "Поэтому я счёл за лучшее не спорить и делать то, что мне говорит эта бандитка."
    "Кроме того, она напоминала мне одного крайне знакомого человечка…"
    scene bg ext_aidpost_day at zexitcenter2 with flash
    "Моего рыжего ангела, спорить с которым порой было весьма опасно для здоровья."
    "Притом, что она была сантиметров на десять ниже меня, она шла — именно шла! — так, что мне приходилось бежать за ней."
    am "Эй, куда ты меня тащишь?!"
    dv "Заткнись, я сказала!"
    "Рявкнула рыжая."
    dv "Или получишь!"
    "Я только что пришёл в себя в непонятном месте, да ещё и летом."
    "За последний час меня ударили по голове, облапали и наорали."
    "И оттого в голове царил сущий хаос — про вещи типа гордости или силы воли даже не вспоминалось…"
    "Злость девушки была довольно забавной, но кто его знает, на что она способна?"
    "И, как всегда, оказавшись в непонятной ситуации, я прибёг к старому доброму способу решения проблем."
    "Я пустил всё на самотёк."
    "Позволил увлечь себя вперёд, чуть прищурился и {i}ощутил{/i} нечто странное."
    "Запах-не запах, скорее, некое ощущение, весьма знакомое."
    "Возможно, дело было в том, что она действительно была довольно похожа на одну полузабытую знакомую из знакомого детства."
    "Но из того же детства я помнил, что всю жизнь видел от неё только хорошее."
    th "Возможно, и эта такая же?"
    "Пусть говорят, мол, рыжий да красный — человек опасный."
    scene bg ext_square_day
    with flash
    "Отбежав на достаточно приличное расстояние от здания медпункта, мы оказались на какой-то площади."
    "Всё как обычно — флагштоки, трибуна."
    "И…"
    am "Генда."
    th "Ха, опять тётя Оля позвала в гости, а я очнулся раньше времени?"
    "Лагерь выглядел смутно знакомым, пусть и не во всёх, ох и не во всём."
    "Девушка, не поворачиваясь, дёрнула плечом."
    "Мы пошли вровень."
    scene bg ext_square_day
    show dv normal pioneer2
    with dissolve
    "К этому моменту я был мокрый как мышь, под своими тремя одёжками, не предназначенными для ношения летом."
    "Ну, или хотя бы не в тридцатиградусную жару!"
    am "И… Зачем ты меня сюда притащила?"
    dv "Разве ты не слышал?"
    "Девушка дышала ровно и легко."
    dv "Тебя сказали отвести к вожатой."
    dv "Вот и пошли."
    "Мне не очень понравился нездоровый энтузиазм у неё в глазах, но я решил пока не делать поспешных выводов."
    stop music fadeout 3
    stop ambience fadeout 2
    scene bg ext_boathouse_day
    with dissolve
    play ambience ambience_boat_station_day fadein 5
    "Ещё несколько минут пешком — и мы вышли на пристань."
    "Огромное здание-дебаркадер, которое соединялось с берегом длинными наплавными мостками на понтонах."
    "Много-много лодок, катамаранов и водных велосипедов."
    "И успокаивающий плеск воды."
    "Учитывая то, что пляж был здесь чисто символический, а у зарослей не стояли вездесущие голубые домики-раздевалки, я сделал вывод, что пионеров купают где-то в другом месте."
    am "Вожатая здесь?"
    "Девушка молча кивнула."
    "Делать нечего, оставалось проследовать вслед за ней."
    scene bg ext_boathouse_day at zenterleft
    show dv normal pioneer2 at cleft
    with dissolve
    dv "Мне нужно кое-что сделать для вожатой, поэтому попробуем совместить."
    am "Прости, что?"
    dv "Помолчи."
    "Досадливо отмахнулась она, озираясь."
    "В отличие от неё, я до сих пор чувствовал себя немного стеснённо."
    th "Надеюсь, это не затянется надолго."
    "Загадал я."
    th "Сейчас она сделает то, что ей нужно, и мы…"
    play sound sfx_wood_floor_squeak
    pause(1)
    scene bg ext_boathouse_alt_day_7dl  at zenterleft
    show dv normal pioneer2 at cleft
    with dissolve
    "Наконец, мы заступили на мостки собственно брандвахты — от берега нас отделял уже не один десяток метров."
    show dv smile pioneer2 with dspr
    dv "Вот!"
    "Её лицо просияло."
    am "Что?"
    dv "Не понимаешь?"
    am "Нет."
    show dv surprise pioneer2 with dspr
    dv "Ну ты и придурок!"
    "Она указала на невесть откуда тут взявшуюся банную шайку."
    dv "Хватай её."
    "Указала она на жестяной тазик с ручками."
    "А я подчинился. {w}Что мне ещё оставалось?"
    dv "Теперь… Теперь… {w}Набери в неё воды."
    play music music_list["eat_some_trouble"] fadein 2
    am "Послушай!"
    "Не выдержал я."
    am "Ты что, сама не можешь? {w}Почему я тут как раб твои приказания выполняю?"
    show dv smile pioneer2 with dspr
    dv "Я не поняла, ты хочешь найти вожатую или нет?"
    am "Да я сам быстрее её найду."
    dv "Ладно."
    show dv normal pioneer2 far with dissolve
    "Девушка подозрительно легко отступила — пожала плечами и отошла в сторону."
    dv "Иди-иди. {w}Как устанешь искать, возвращайся."
    "Я заколебался."
    "Включая эту ненормальную, у меня здесь было ровным счётом полтора знакомых: магистр похабных наук Виолетта Церновна и тараторка, которая за болтовнёй забыла представиться."
    "По себе знаю, как раздражает, если кто-то приходит в коллектив со стороны и начинает задавать идиотские вопросы по трёхмиллионному разу."
    "Самым ласковым ответом обычно бывает RTFM да ссылка на часто задаваемые вопросы."
    "Но кто бы здесь ссылочкой подогрел бы?"
    with fade
    am "Блин, нашли мастера на все руки, безотказного и бесполезного."
    "Ворча, я наклонился и попытался зачерпнуть шайкой воду из речки."
    dv "Полную наливай!"
    "Прикрикнула рыжая."
    am "Сейчас."
    am "Погоди-ка…"
    dv "А?"
    am "Протекает, видишь?"
    show dv normal pioneer2 with dspr
    dv "Ничего, так даже лучше. {w}Давай-ка я тебе помогу."
    "Она взяла тазик за вторую ручку."
    "В свободной руке у неё уже была швабра."
    am "Ты мыть что-то собралась?"
    show dv grin pioneer2 with dspr
    dv "Да, что-то вроде того."
    dv "Тут аккуратнее, не ударься об угол."
    "Вдруг предупредила она, притормозив на повороте."
    "Меня тронула её забота."
    "В голову пришло, что она, возможно, не такая уж и злая."
    scene bg ext_boathouse_alt_day_7dl
    with fade
    dv "Пришли."
    "Мы стояли на дальнем правом причальном углу, отделённом от берега громадой здания дебаркадера, здесь крыша нависала над головой до самых перил."
    show dv normal pioneer2 with dspr
    dv "Да, это то самое место."
    dv "Мне надо будет, чтобы ты поднял таз на крышу."
    am "Ладно."
    show dv laugh pioneer2 with dspr
    dv "Сил-то хватит?"
    am "Спрашиваешь! {w}Хватит, конечно!"
    am "Но не думаю, что мне хватит длины рук."
    show dv normal pioneer2 with dspr
    dv "Шваброй подденешь, делов-то."
    am "Шваброй?"
    show dv angry pioneer2 with dspr
    dv "Ты как маленький, честное слово!"
    "Рассердилась девушка."
    dv "Всё объяснять надо."
    dv "Слушай сюда, объясняю один раз: поднимаешь, пока высоты рук хватает, а там поддеваешь щёткой и прижимаешь к потолку. {w}Усёк?"
    am "Но я же так всё разолью!"
    dv "Грррр… Просто сделай это, ладно? {w}Я на первый раз тебе помогу поднять до уровня крыши."
    dv "Потом поднимусь наверх, а ты подтащишь его, чтобы я достать смогла. {w}Всё понял?"
    "Я понятливо кивнул."
    show dv normal pioneer2 with dspr
    "И несколько секунд спустя шайка оказалась плотно прижатой под днище к потолку над нами."
    "Сказать по правде, даже с опытным наставником поднять её оказалось довольно проблематично — кривые ручки, кривая швабра."
    "Я постоянно терял равновесие."
    "Но теперь, когда вся конструкция оказалась прижатой к потолку, я чувствовал себя несколько увереннее."
    "Разве что смущало подтекающее дно таза, откуда на меня капала вода."
    dv "Всё, держи. {w}Я скоро."
    show dv smile pioneer2 with dspr
    hide dv with dissolve
    "Она улыбнулась и убежала."
    th "Надеюсь, она не очень долго будет залезать."
    "Несмотря на то, что вес был не очень большой, держать его на полусогнутых было тяжеловато."
    "Так что минут десять, может, простою, не больше."
    th "А с другой стороны, интересно проверить, насколько я выносливый."
    "Так что если она немного задержится, так, может, и будет лучше."
    "Я бы попробовал выстоять минут пятнадцать."
    "Чтобы не попрекали, мол, ничего тяжелее компьютерной мышки да планшета и в руках-то не держал."
    "Так что распределяем нагрузку — выпрямляем спину, расправляем плечи, ноги ставим на ширине плеч."
    play music music_list["you_lost_me"] fadein 3
    "И стоим."
    "Стоим."
    "У меня не было возможности проверить время и шум волн действовал расслабляюще, поэтому время тянулось надоедливо медленно."
    th "Интересно, уже прошло десять минут или ещё нет?"
    "Лениво подумал я, пытаясь найти позу, в которой вода из тазика не будет капать мне за шиворот."
    $ set_mode_nvl()
    th "Интересно, как будет выглядеть путь на крышу?"
    "Я представил себе, как девушка вприпрыжку идёт к ответственному человеку, наверное, какому-нибудь сторожу."
    "До берега можно дойти за примерно тридцать шагов, потом ещё столько же вверх по песку, до помещения сторожки."
    "Теперь открыть дверь, поздороваться и зайти."
    "После этого нужно чётко и внятно объяснить сторожу — что тебе от него надо и зачем."
    "А нужна нам либо лестница на крышу, либо ключи от чердака."
    "Зачем? А хотим крышу отмыть… Эээ, например от помёта птиц."
    "Сторож немного сомневается, но ключ или лестницу даёт."
    nvl clear
    "Теперь всё то же самое, но в обратной последовательности."
    "Но не забываем, что в руках у девушки ценный груз, не терпящий спешки."
    "А надо ещё забраться на крышу — а это ещё пять минут."
    "Теперь либо перевалить конёк, либо открыть слуховое окошко, чтобы оказаться с моей стороны…"
    "И, наконец, окликнуть меня!"
    nvl clear
    $ set_mode_adv()
    "Когда я прокрутил эту последовательность в мозгу в пятый раз, я начал немного беспокоиться."
    "не то чтобы в душу начали закрадываться сомнения, но…"
    th "Что там можно делать так долго?"
    "Даже по самым оптимистичным прикидкам, рыжая где-то лазила минут двадцать."
    "Время, достаточное для того, чтобы неспешно сделать абсолютно всё необходимое."
    with dissolve
    "Руки устали и начали затекать."
    "Я даже уже не пытался уворачиваться от капель — какая разница."
    "Я просто ждал, когда же наконец у меня заберут чёртову шайку, и мы пойдём к вожатой."
    th "Что её так задержало?"
    "В моём вопросе было целое море неуверенности."
    th "Не бросила же она меня тут, в самом-то деле!"
    "Но кто знает их нравы морали…"
    with fade
    play sound sfx_7dl["eat_horn"] fadein 1
    "На улице начало вечереть — в напоённом ароматом хвои воздухе разнёсся сигнал горна, зовущий всех на приём пищи."
    "Судя по всему, что-то вроде файф-о-клока или полдника на советский манер."
    "А я стою здесь."
    "И вода льётся мне за шиворот."
    "А я даже не могу опустить шайку вниз — с моим косоручием она обязательно сыграет мне по голове."
    "Тем более, что центр тяжести непонятно где, стало быть, полетит таз согласно исключительно законам Мэрфи."
    "То есть, в меня."
    th "Вот и помог."
    "Скрипнул зубами я."
    th "Напыщенный индюк, придурок, мускулами поиграться захотел, испытание выносливости устроил."
    th "С самого начала надо было догадаться, что с таким паскудным выражением на лице никто и никогда не занимается общественно-полезной деятельностью."
    "Решив, что хуже, чем было, уже не будет, я попытался позвать на помощь:"
    am "Эгегей! Люди!"
    am "Есть здесь кто-нибудь? Мне нужна помощь!"
    th "Позор какой, господи."
    th "Пусть это будет кто-то один, не слишком болтливый!"
    th "И он спасёт меня, и никому ничего не скажет, и у нас будет братство взаимовыручки!"
    "Руки затекали всё сильнее, а отзываться никто не спешил."
    th "Кажется, вечером здесь действительно никого не бывает."
    th "Что же делать теперь…"
    "На каждую руку будто по гире навесили."
    "Плечи и предплечья уже практически не чувствовались, и я держался на одном упрямстве, продолжая бессвязно выкрикивать просьбы о помощи."
    "В практике йоги есть понятие асаны, когда тело уравновешивается в неподвижности, тем самым разгружая подсознание, обычно отвечающее за поддержание равновесия и микродвижения тела."
    "Это позволяет высвободить скрытые резервы и полностью посвятить себя медитации."
    "Но я чувствовал, что ещё пару минут, и я постигну дзен {w}— и плевать мне будет на то, что произойдёт с водой, тазиком и мной."
    "Надежды на спасение не было."
    "Чудесный супермен не имеет обыкновения появляться из ниоткуда и спасать мне подобных."
    stop sound fadeout 3
    show blackout_exh3 with fade
    "Вот и приходится рассчитывать только на себя."
    "И получать свои шишки в полном объёме."
    "Может быть, если бы я среагировал на ситуацию чуть раньше, всё было бы иначе."
    "У меня не болела бы спина и бёдра."
    "Наверное, я бы смог рассчитать всё так, чтобы увернуться от большей части воды."
    "Но не сейчас."
    "У меня не было другого выбора."
    "Кроме как принять неизбежное."
    stop music fadeout 3
    "На показавшуюся бесконечной долю секунды таз замер в воздухе, будто до сих пор прилипая краями к крашеному дереву потолка."
    "А потом, развернувшись"
    play sound2 sfx_water_emerge
    extend ", устремился вниз."
    "Первая ступень унижения."
    "С крыши тут же донёсся жизнерадостный смех, и рядом со мной приземлилась она."
    scene bg ext_boathouse_alt_day_7dl at zenterleft
    show dv grin pioneer2 at cleft
    with dissolve
    play music music_list["doomed_to_be_defeated"] fadein 0
    "Рыжая."
    "Лидер списка «10 людей, которые должны сдохнуть»."
    "Подставившая меня гадина."
    "В голову вдруг пришло, что я так и не узнал её имени."
    show dv laugh pioneer2 with dspr
    dv "А ты молодец! Молодец!"
    dv "Дольше всех продержался, я даже успела сбегать пополдничать."
    dv "Ещё немного, и я бы помогла тебе."
    "Я стоял в тёплой одежде, насквозь мокрой, с меня ручьями текла вода."
    "Так что лучше сохранять здравомыслие и спокойствие в такой ситуации."
    show dv scared pioneer2
    show alt_credits "ПОШЛА ВОООООН!":
        pos (747,250)
    with Shake((0, 0, 0, 0), 2.0, dist=50)
    pause(2)
    scene bg ext_boathouse_alt_day_7dl with flash
    show dv surprise pioneer2 at cleft with dspr
    am "И не подходи ко мне больше."
    "Меня можно понять."
    "Попасть непонятно куда непонятно как и в результате оказаться жертвой розыгрыша."
    "Так что я был несколько расстроен."
    show dv shocked pioneer2 far at cleft with dspr
    dv "Ого."
    "От удивления незнакомка отошла на шаг назад."
    "Кажется, её впечатлили мои вокальные данные."
    dv "Ты чего нервничаешь? {w}Псих, что ли?"
    show dv angry pioneer2 far with dspr
    dv "Если псих, то съешь таблетку!"
    "Надо воздать должное рыжей, восстанавливалась она быстро."
    "Интересно, как быстро она восстановится, если я её придушу?"
    "Это была очень, очень интересная идея."
    "Несколько секунд я всерьёз её рассматривал."
    "Кажется, на моём лице отразилась часть мыслей на тему, так как рыжая не спешила подходить ближе."
    show dv normal pioneer2 far with dspr
    "Она так и стояла у воды, опираясь о поручни, и если бы дело происходило в каком-нибудь дешёвом ситкоме, я мог бы пинком сломать этот поручень."
    "Она бы полетела в воду, а за кадром включили бы дебильный смех."
    "Жаль, что мы не в дешёвом ситкоме."
    dv "Ну, успокоился?"
    "Наконец нарушила молчание она."
    "Никак не могу понять: она уже поиздевалась надо мной, что ей ещё нужно?"
    am "Значит, вот так ты заводишь себе врагов?"
    show dv laugh pioneer2 with dspr
    dv "Ты должен был видеть своё лицо, когда понял, что я не приду."
    dv "Это бесценно!"
    show dv normal pioneer2 with dissolve
    "Кидало мою новую не-знакомую из крайности в крайность, от надменности до панибратства."
    am "А если бы ты оказалась на моём месте?"
    th "Хотя для девушки оказаться в мокрой рубашке это, всё-таки, немного другое."
    am "У меня и переодеться не во что."
    show dv grin pioneer2 with dspr
    "Она ухмыльнулась."
    dv "Сам виноват! {w}Нечего было опаздывать на смену!"
    am "Я вообще здесь быть не должен!"
    "Я не могу понять, почему эта девушка ведёт себя так, будто мы давным-давно знакомы?"
    "Не опасается, что я пойду вразнос."
    "Будто знает, на что я способен."
    th "Или просто такая безбашенная?"
    am "Я вообще…"
    "Я осёкся."
    th "Допустим, скажу я, что заснул… а проснулся в медпункте, да ещё не зимой, а летом?"
    th "Кому от этого легче станет? Санитарам психушки?"
    "Девушка стояла и улыбалась мне, будто ожидая, что я её сейчас похвалю за невероятно смешную шутку."
    am "Больная идиотка."
    "Пробормотал я себе под нос."
    show dv smile pioneer2 with dspr
    dv "Прости, что?"
    "Приветливо улыбнулась она."
    "Я проигнорировал её, старательно отжимая свитер."
    dv "Я колебалась между розыгрышем с сажей на лице и тазиком."
    dv "Но пожалела тебя."
    am "О, спасибо огромное."
    am "Но ты хотя бы можешь мне сказать — за каким лядом ты это сделала? {w}Я что, успел оттоптать тебе любимую мозоль?"
    show dv angry pioneer2 with dspr
    dv "Думаешь, я не помню, о чём вы тогда говорили?!"
    am "Когда?"
    dv "В медпункте!"
    show dv shy pioneer2 with dspr
    extend " Ой…"
    th "Когда она подслушивала, а Виолетта её поймала?"
    th "Значит, на мне сорвали злость?"
    am "Ох и стерва же ты!"
    show dv rage pioneer2 with dspr
    dv "Что {w}ты {w}сказал?!"
    am "Ничего."
    "Отрезал я."
    am "Я прекрасно понимаю все неписанные правила, поэтому стараюсь на тебя не злиться."
    am "Но я не обязан быть в восторге от незапланированного купания."
    sl "Алиса!"
    $ meet('dv', 'Алиса')
    "Вдруг закричал кто-то из-за моей спины."
    stop music fadeout 3
    sl "Опять над новичками издеваешься?!"
    scene bg ext_boathouse_day at zenterleft
    show dv angry pioneer2 at cright with moveinright
    show sl angry pioneer at left
    with dissolve
    "Алиса мгновенно замкнулась и сделала независимый вид."
    scene
    $ renpy.show("bg ext_boathouse_alt_day_7dl", at_list = [zenterright], what = Noon("bg ext_boathouse_alt_day_7dl"))
    show sl smile pioneer at right
    with dissolve
    show name_sl with flash:
        pos(760,480)
        linear 1.0 pos (670,420)
    pause(2)
    scene bg ext_boathouse_alt_day_7dl
    show dv angry pioneer2 at left
    show sl angry pioneer at right
    with dissolve
    dv "Ты дремучая, не понимаешь, что есть традиции, которые нельзя нарушать?"
    dv "Новичков надо встретить, провести им крещение."
    sl "Не заговаривай мне зубы, я всё Ольге Дмитриевне расскажу."
    dv "Да рассказывай, жалко, что ли."
    "Алиса, держа руки всё так же за спиной, независимо пожала плечами."
    "Смерила неприязненным взглядом кричавшую девушку."
    hide dv with easeoutright
    "И, развернувшись, неторопливо направилась по своим крайне важным рыжим делам."
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_S:
    play music music_list["take_me_beautifully"] fadein 1
    scene bg ext_boathouse_day at zenterleft
    show sl normal pioneer at cleft
    with dissolve
    "Новое действующее лицо тоже оказалось симпатичным."
    "Но по-своему, конечно, по-своему."
    "Это, если мне позволено сравнивать, был образчик деревенской красоты, незамутнённая славянская кровь."
    "Представься она Василисой или Ксенией, я бы не удивился, ей здорово бы подошли эти имена."
    "Высокая, голубоглазая, с двумя пшеничными косами."
    "На ней куда уместнее смотрелся бы классический сарафан, нежели пионерская форма, но в чужой монастырь…"
    am "Э… Привет."
    "Проводив взглядом удаляющуюся Алису, я повернулся к блондинке."
    show sl sad pioneer with dspr
    "Она почему-то выглядела грустной."
    sl "Облила, да?"
    am "Ну…"
    sl "Ты весь мокрый, это она тебя?"
    am "Вообще-то, это я сам."
    "Я описал ситуацию, в которую угодил по вине Алисы: с тазом, шваброй и ожиданием."
    "В голову вдруг пришло, что пока я тут упражнялся в стойкости, меня, наверное, искали…"
    am "Так что я теперь здесь, такой вот молодой и красивый."
    "Ситуация — хуже не придумаешь."
    "Хорошо хоть пальто со своей машинерией оставил в медпункте, и на них не попала вода."
    show sl surprise pioneer with dspr
    sl "Дела…"
    "Протянула девушка."
    sl "Но тебе нельзя в мокром ходить, ты же простудишься!"
    sl "Пойдём!"
    "Она потянула меня за руку."
    am "А?"
    show sl normal pioneer with dspr
    sl "У тебя формы же нет, как я понимаю? {w}У меня есть запасная мальчишеская, должна быть тебе впору."
    "Взгляд у неё был намётанный."
    sl "Заодно и в сухое переоденешься. {w}А там и к Ольге Дмитриевне пойдём."
    am "А это кто?"
    show sl smile2 pioneer with dspr
    sl "Вожатая первого отряда. {w}Пошли, потом поговорим!"
    "Как и Алиса, эта девушка совершенно не постеснялась взять меня за руку."
    stop music fadeout 3
    scene bg ext_square_day with dissolve
    play ambience ambience_camp_center_day fadein 3
    play music music_list["sweet_darkness"] fadein 5
    "Было не очень-то приятно идти в таком виде через весь лагерь, где меня кто-то может увидеть."
    "Но деваться было некуда, если я попытаюсь взбрыкнуть, то обязательно попаду в какую-нибудь переделку."
    "Да и лёгкий ветерок, периодически выстужающий мокрую одежду, лишал воли к сопротивлению."
    "Мокрый свитер я снял и выжал на ходу, повязав его на поясе по-походному, но футболка оставалась мокрой."
    "Спасибо моей доброй провожатой, она вела меня не самыми людными маршрутами, видимо, понимая ситуацию."
    "К счастью, конечная точка нашего пути оказалась не слишком далека."
    scene bg ext_house_of_sl_day at zenterleft
    show sl normal pioneer at cleft
    with dissolve
    sl "Пришли! {w}Подожди здесь, пожалуйста, я посмотрю, есть ли соседка дома."
    hide sl with dissolve
    "Она ненадолго скрылась за дверью, а через минуту уже поманила меня пальцем."
    sl "Заходи!"
    play sound sfx_open_door_clubs_2
    scene bg int_house_of_sl_day with dissolve
    play ambience ambience_int_cabin_day fadein 3
    "Оказавшись внутри, я с любопытством огляделся."
    am "Интересно у вас тут."
    scene bg int_house_of_sl_day at zenterleft
    show sl smile pioneer at cleft
    with dissolve
    sl "Да? {w}Спасибо. Я стараюсь придать помещению жилой вид, где это возможно, но сам понимаешь."
    "Наконец, она вытащила из шкафа комплект пионерской формы и кинула его мне."
    play sound sfx_7dl["blanket"] fadein 0
    sl "Надеюсь, не ошиблась с размером. {w}Одевайся."
    am "Э… Ты не могла бы…"
    hide sl with dissolve
    "Девушка понятливо улыбнулась и открыла дверцу шкафа, разделяя себя и меня."
    sl "Обещаю не подглядывать, одевайся."
    "Рассмеялась она."
    "В отличие от Алисы, эта девушка смеялась беззлобно и от души, не пытаясь как-то унизить собеседника."
    am "Так значит, Алиса у вас главный источник неприятностей?"
    am "Или мне ещё ждать «посвящений»?"
    "Воюя с кургузым ремешком, спросил я."
    sl "Вообще, у нас есть бедствие и похуже Алисы."
    th "И такое бывает?"
    am "Это как?"
    sl "Да… Понимаешь, во втором отряде не хватило места, а Алиса попросила перевести подругу к нам."
    sl "Тогда мы ещё не знали ни Алису, ни её подругу —  всё по разным отрядам были…"
    "Представив себе масштабы кошмара, на который способны две Алисы сразу, я ужаснулся."
    sl "Ой, я не должна говорить про них плохо. {w}Они не плохие, не злые."
    "Создавалось впечатление, что девушка говорит об этом как о чём-то, в чём она заинтересована лично."
    sl "Просто очень беспокойные и любят подшучивать над окружающими."
    am "Что, и над тобой тоже? Вспомнишь что-нибудь?"
    "Судя по голосу, девушка явно смутилась."
    th "Кажется, «что-нибудь» она определённо вспомнила."
    sl "Нет, что-то ничего в голову не приходит."
    "Неловко произнесла она, прячась за дверцей."
    th "Но мне она точно не скажет."
    "Пока суд да дело, я полностью переоделся."
    "Глаз у девушки действительно оказался намётанный — нигде не жало и не висело, всё было впору."
    am "Я закончил."
    show sl smile pioneer with dspr
    sl "Вот и здорово! Посмотришь на себя?"
    "Она кивнула на шкаф."
    sl "Там со внутренней стороны зеркало."
    "Кивнув, я принял предложение."
    "И, как показала практика — напрасно."
    play music music_list["revenga"] fadein 3
    scene cg d2_mirror:
        xalign 0.55 yalign 0.45 zoom 1.25
        linear 2.0 zoom 1.0 xalign 0.45 yalign 0.55
    with Shake((0, 0, 0, 0), 2.0, dist=50)
    pause(2)
    th "Ох и {image=alt_KS_censor} же я вас за ваше {image=alt_KS_censor2} {image=alt_KS_censor} с {image=alt_KS_censor2} {image=alt_KS_censor} {image=alt_KS_censor}."
    th "Пробурчал поручик, а затем грязно выругался."
    "Только то, что я за сегодня, кажется, исчерпал свой лимит истерик и падений в обморок, да остатки невесть откуда взявшегося чувства собственного достоинства…"
    "С самого момента пробуждения в медпункте я всеми силами пытался удержаться на краю разума."
    "Придумывал логичные и рациональные объяснения всему, что встречалось, от обстановки до пионеров."
    "Даже лету."
    "Если потрудиться, всему этому до поры находилось достойное оправдание."
    "Но должны же быть пределы гибкости психики!"
    "Даже если незадолго до этого добрая доктор Виолетта накормила меня волшебной таблеткой."
    "Я говорил что-то про моё невероятное спокойствие? Забудьте!"
    "То, что смотрело на меня из зеркала, было мертвецом."
    "Труп его был похоронен десять лет назад, скелет до блеска отполирован червями."
    "Я убил его собственными руками, выбрасывая последние контакты из телефонов и переезжая на новое место жительства."
    "Это же я."
    "Образца десятилетней давности."
    stop music fadeout 3
    pause(1)
    scene anim prolog_1 with fade2
    play music music_7dl["rewind"] fadein 3
    "Наверное, каждый из нас хотя бы раз в жизни задумывался о такой вещи, как второй шанс."
    "Существует два типа людей из тех, что не сидят на месте ровно: первые пытаются построить сияющее будущее, вторые пытаются искупить грехи прошлого."
    "Я слишком долго оставался в стороне."
    "Но было бы глупо отрицать, что я тоже задумывался о том, что если бы однажды у меня появился шанс повернуть время вспять и в тот самый день выбрать совершенно другое — как многое можно было бы исправить!"
    "Человек в сослагательном наклонении подобен богу, он может видеть всё, может понимать где и что неправильно делает, но…"
    "Он ничего не может исправить. Ровным счётом ничего. {w}На то оно и сослагательное наклонение."
    "И то, о чём ты потом жалеешь всю жизнь, остаётся с тобой до конца твоих дней."
    "Потому что прошлое неизменно, ты не молодеешь, и все пути ведут в могилу."
    scene bg int_bus
    show prologue_dream
    with dissolve
    "Мне всю жизнь снился один сон."
    "Иногда раз в месяц, иногда раз в год."
    "Но я помнил его совершенно отчётливо."
    "Там была девушка."
    "Она постоянно звала меня куда-то, а я каким-то десятым чутьём понимал, что если я приму протянутую руку, то пути назад уже не будет."
    "Я привык к тому, что если мне снится этот сон, то проснусь больной и разбитый, меня будут с неделю преследовать мелкие неудачи и так далее."
    "Поэтому ничего удивительного в том, что я считал её предвестником грядущих неприятностей и никогда, никогда не принимал её предложений."
    "До недавнего времени, пока не оказался загнан в угол…"
    scene bg int_bus
    show uvao_d1 at left
    show prologue_dream
    with flash
    dreamgirl "Ты бы не смог вернуться…"
    am "Что?"
    dreamgirl "Ты здесь не просто так."
    "Я понял, что каким-то образом оказался в автобусе."
    "И надо мной склонилась та самая девушка из сна."
    am "Это ты привезла меня сюда?"
    dreamgirl "Не совсем."
    "Она слабо улыбнулась."
    dreamgirl "Я просто помогаю."
    am "Помогаешь кому?"
    dreamgirl "Я не знаю."
    "Снаружи послышался шум, и девушка встревоженно оглянулась."
    dreamgirl "Тебе, наверное."
    dreamgirl "У нас мало времени."
    scene cg d1_uv_bus_7dl
    show prologue_dream
    with flash
    dreamgirl "Это должно задержать их ненадолго."
    "Автобус тряхнуло, и я невольно выставил руки вперёд, стараясь поймать пошатнувшуюся девушку."
    dreamgirl "Не трогай!" with vpunch
    scene bg int_house_of_sl_day
    show uvao_d1 at left
    show prologue_dream
    with flash
    "Она рывком отскочила, но в мире уже что-то изменилось."
    dreamgirl "И почему ты такой проблемный? {w}Теперь снова всё исправлять."
    "Вздохнула девушка."
    "Она ненадолго замерла, прислушавшись, по-кошачьи склонив голову набок, а потом махнула рукой:"
    dreamgirl "А, и ладно. Тебе всё равно скоро просыпаться."
    dreamgirl "Ты уже получил письмо?"
    am "Письмо? Какое?"
    dreamgirl "Не важно. {w}Когда ты увидишь его, поймёшь сразу."
    "Оказалось вдруг, что я лежу на кровати, а девушка присела на её край и легонько растрепала мои волосы."
    dreamgirl "Как бы то ни было — это зд{b}о{/b}рово, что ты здесь."
    am "Что за дурацкие правила? Тебе, значит, можно меня касаться?"
    dreamgirl "Прости. Взамен могу исполнить одно твоё желание или ответить на один твой вопрос."
    am "С желаниями плоховато, я прямо сейчас ничего не хочу."
    am "А вот вопрос…"
    with fade
    am "Как тебя зовут?"
    play sound sfx_ghost_children_laugh
    dreamgirl "Какой глупый вопрос…"
    "Её смех звучал, как могла бы звучать музыка, исполненная несуществующим музыкантом."
    "Я засомневался в его существовании раньше, чем он смолк."
    am "И всё-таки! Ответь! Как тебя зовут."
    dreamgirl "Ты обязательно вспомнишь."
    "Она снова растрепала мне волосы."
    pause(2)
    stop music fadeout 5
    play ambience ambience_int_cabin_day fadein 2
    scene bg int_house_of_sl_day with dissolve
    "И исчезла."
    am "Ау? {w}Ты где?"
    "Кажется, я сам не заметил, как заснул, а она пропала."
    sl "Я здесь."
    play music music_list["my_daily_life"] fadein 3
    show sl scared pioneer close with dissolve
    "Рядом с кроватью, на которой я лежал, появилась девушка, которая снабдила меня формой."
    "Выглядела она не на шутку напуганной."
    sl "Ты… Что с тобой случилось?"
    am "Ты не видела? Я же…"
    th "Помолодел на десять лет."
    "Меня очень серьёзно «откатило» в физическом плане."
    "А вот память осталась моей… Ну, насколько это было возможно."
    "Плюс то, что я чувствовал, насколько знакомы мне все встречные девушки, но… Хоть убей."
    "Так что, думаю, не стоит кричать об этом на всех углах."
    show sl sad pioneer close with dspr
    sl "Да, да, доктор сказала, что у тебя реакция на долгий переезд и на жару."
    am "Ага, на них. {w}Как долго я был в отключке?"
    sl "Полчаса, наверное — я как раз успела перенести тебя на кровать и сходить предупредить вожатую."
    show sl smile pioneer close with dspr
    "Девушка улыбнулась."
    sl "Надеюсь, ты скоро привыкнешь и тебе у нас понравится."
    "Я обратил внимание на то, что до сих пор нахожусь в том самом домике, где переодевался."
    show sl shy pioneer with dspr
    "Девушка немного смутилась, поняв невысказанный вопрос."
    sl "Да, ты у меня. {w}Ты тяжёлый, я бы тебя далеко не унесла."
    sl "Пришлось положить тебя пока к себе."
    sl "Как-никак, я помощница вожатой и отвечаю за пионеров."
    menu:
        "Приятно познакомиться":
            show sl smile2 pioneer with dspr
            "Вежливо кивнул я, а девочка снова улыбнулась."
            sl "Прости, где мои манеры."
            "Она кивнула и представилась наконец:"
            sl "Меня зовут Славяна, Феоктистова Славяна."
            $ meet('sl', 'Славяна')
            sl "Но ты, если хочешь, можешь звать меня Славей — я уже привыкла."
            $ meet('sl', 'Славя')
            $ alt_day1_me_d3_sl_met = True
            $ lp_sl += 1
        "Да, спасибо":
            "С третьего щелчка вышибло ум у старика."
            "Пионерки, бронзовые деятели и советский флаг — всё вместе или по отдельности ещё можно было как-то объяснить."
            "Но резкое омоложение…"
            show sl normal pioneer with dspr
            sl "Может быть, я тебя к доктору отведу?"
            "Обеспокоенно предложила девушка."
            sl "Ты какой-то бледный."
            am "Нет, спасибо."
            "Клин клином вышибают."
            "Воспоминания о том, как местный доктор лечит пациентов, помогли немного прийти в себя."
    "Мы немного помолчали каждый о своём."
    menu:
        "А ты сейчас занята?":
            $ lp_sl += 1
            $ alt_day1_me_d3_sl_conv = True
            sl "Прямо сейчас я за тобой смотрю."
            "Улыбнулась девушка."
            am "Да я себя неплохо чувствую, просто…"
            th "Просто я опять превратился непонятно в кого и непонятно где!"
            am "Нельзя делать резкие движения, вот и всё."
            show sl sad pioneer with dspr
            sl "Уверен?"
            "Кажется, её не убедили ни мои слова, ни мой тон."
            am "Давай мы просто уже к вожатой сходим и представим меня."
            am "А то не могу отделаться от впечатления, что я каким-то образом подвожу тебя, пока здесь валяюсь."
            "Немного поколебавшись, девушка кивнула."
            show sl normal pioneer with dspr
            sl "Хорошо, пошли."
            "Она погрозила мне пальцем."
            sl "Но если вдруг что — опирайся на меня, и потом пулей к доктору!"
            sl "Договорились?"
            "Я кивнул."
            sl "Тогда ладно."
            "Она достала из шкафа пару новеньких сандалет."
            sl "У нас обычно обувь у двери оставляют, так что имей в виду."
            sl "Обувайся и пошли."
            sl "Да… И причешись."
            "Я растерянно поднял ладонь к макушке…"
            th "Так это был не сон?"
            play sound sfx_open_dooor_campus_1
            pause(1)
            stop music fadeout 3
            scene bg ext_house_of_sl_day at zenterright
            show sl normal pioneer at cright
            with dissolve
            "Однако далеко мы не ушли."
            "Не успели мы сделать и пяти шагов, как нас окрикнули."
            scene bg ext_house_of_sl_day at enterleft
            show sl normal pioneer at cright with dspr
            show mi smile pioneer at left with moveinleft
            mi "Славичка, подожди, Славичка!"
            if not alt_day1_me_d3_sl_met:
                "Девушка поморщилась и, опустив голос, сообщила:"
                show sl shy pioneer at right with dspr
                sl "Это моё имя — Славя."
                $ meet('sl', 'Славя')
                $ alt_day1_me_d3_sl_met = True
                am "Очень приятно."
                sl "А я знаю."
            mi "Славичка, Славичка, там у столовой опять третий отряд девчонок шиповником закидал, помоги пожалуйста, а то я пройти хочу… а боюсь."
            "Славя вздохнула."
            sl "Вообще, я…"
            am "Да всё нормально, я подожду сколько скажешь."
            sl "А Лена где?"
            mi "Как обычно, сидит на крыльце и рисует, прислать?"
            sl "Да, передай ей пожалуйста, пускай новичка проводит и представит вожатой."
            mi "Бу сделано!"
            hide mi with easeoutleft
            "Сверкнув улыбкой, тараторка убежала."
            th "Как там её Славя назвала? {w}Не помню."
            hide sl with easeoutright
            "Я повернулся было к Славе уточнить, но той и след простыл."
            "Делать нечего, пришлось занять место на крыльце и ждать некую Лену."
            stop sound_loop
        "Я немного отдохну, не против?":
            sl "Конечно! Лежи пока."
            sl "У меня кое-какие дела по лагерю, так что я побежала, а к тебе провожатого пришлю."
            sl "Чтобы ты не потерялся на пути к вожатой."
            "На секунду показалось, что она подшучивает надо мной — но нет."
            sl "И приведи себя в порядок."
            "Она кивнула мне на расчёску, лежащую здесь же на столе."
            am "А… Можно?"
            "Макушка ещё хранила ощущение прикосновения, а волосы и правда были встрёпанными."
            th "Кажется, она мне не приснилась…"
            sl "Не стесняйся. {w}Ну, я пошла!"
            if not alt_day1_me_d3_sl_met:
                am "Погоди!"
                sl "Да?"
                am "Слушай… Мы же так и не познакомились."
                show sl laugh pioneer with dspr
                sl "Твоё-то имя я знаю! {w}Но ты прав."
                "Она коротко кивнула и представилась:"
                sl "Феоктистова Славяна, но лучше просто Славя."
                $ meet('sl', 'Славя')
                $ alt_day1_me_d3_sl_met = True
                am "Вот и познакомились."
            "Помахав мне рукой, девушка убежала, а я остался отдыхать."
            hide sl with dissolve
            play sound sfx_knock_door7_polite
            pause(1)
            "Десять минут спустя в дверь деликатно постучали."
            am "Войдите!"
            "Веселясь крикнул я."
            "Было очень забавно чувствовать себя хозяином в чужом помещении."
            "Хотя мне было несколько неуютно лежать на кровати в девичьем домике, мало ли кто что подумает, но…"
            "Дверь осталась недвижимой, никто не зашёл, никто не вышел."
            th "Показалось?"
            play sound sfx_knock_door7_polite
            pause(1)
            "Десять минут спустя стук в дверь повторился."
            am "Да войдите же!"
            "Позвал я."
            "Ни ответа, ни привета."
            "В ожидании я даже начал было подрёмывать."
            "И тут стук в дверь повторился в третий раз."
            play sound sfx_knock_door7_polite
            pause(1)
            th "Да они там издеваются, что ли?"
            "Я был готов поклясться, что это сто процентов Алиса балуется, вычислила, куда меня отвели, и теперь мстит."
            "Ну или ещё что-то в этом роде."
            "Со стоном я отскрёб себя от кровати и поплёлся открывать."
            play sound sfx_open_door_strong
            "Уже предвкушая то, как я сейчас с душой рявкну, я распахнул дверь и…"
            "Воздух комом стал у меня в горле."
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_L:
    play ambience ambience_camp_center_evening fadein 3
    if alt_day1_me_d3_sl_conv:
        "Я хотел было подремать, сидя на тёплых ступеньках, но не прошло и десятки минут, как интуиция подала сигнал о том, что рядом кто-то есть."
        show unblink
    play music music_7dl["take_my_hand"] fadein 5
    scene
    $ renpy.show("bg ext_house_of_sl_day", at_list = [zenterleft], what = Noon("bg ext_house_of_sl_day"))
    show un smile2 pioneer at cleft
    with dissolve
    show un smile pioneer at cleft with dspr
    show name_un with flash:
        pos(1060,480)
    show un shy pioneer at cleft with dspr
    pause(2)
    scene bg ext_house_of_sl_day
    show un shy pioneer at cleft
    with dissolve
    if alt_day1_me_d3_sl_conv:
        "И правда."
        "Рядом со мной стояла какая-то девушка."
    else:
        "На пороге стояла какая-то незнакомая девушка, уже заносящая руку для очередного стука."
        "Бум!" with vpunch
        un "Ой!"
        "И снова в лоб."
        "Я с огромным трудом удержался от потока нецензурной брани."
        "Но когда я разглядел, кто меня стукнул… Это искупило всё."
        "Девушка отступила."
    "Её щёки мгновенно вспыхнули румянцем — который, надо признать, шёл ей чрезвычайно."
    "И сама девушка была крайне привлекательной особой — невысокого роста, хрупкая, с огромными зелёными глазами и забавной детской причёской, собиравшей волосы в два хвоста."
    if alt_day1_me_d3_sl_conv:
        un "…"
        "Она набрала в грудь воздуха и мужественно начала:"
    else:
        un "П-прости… Больно?"
        "Нерешительно произнесла она."
        am "Да нет…"
    show un scared pioneer with dspr
    un "А ты…"
    show un sad pioneer with dissolve
    "На этом, кажется, силы покинули её."
    am "Я… Я."
    am "Вряд ли у вас много новеньких."
    am "А как тебя зовут?"
    show un shy pioneer with dissolve
    un "Л-лена."
    $ meet('un', 'Лена')
    "Прошелестела она."
    "Девушка воплощала собой стеснение и смущение."
    "Я с трудом слышал её, даром что стоял рядом, и она стояла глаза в пол, избегая моего взгляда."
    th "Что это с ней? Я думал, здесь все деятельные и живые."
    "Во всяком случае, из четырёх человек, что я здесь встретил, каждый был именно таким."
    "Она махнула рукой в сторону дорожки, мощённой бетонными плитами."
    show un smile pioneer with dspr
    un "П-пойдём… {w}К вожатой?"
    "Несмело улыбнулась она."
    hide un with blind_r
    "И, не дожидаясь моего ответа, пошла, двигаясь деревянно и неестественно, будто марионетка."
    "Кажется, ей и правда было очень неловко."
    play ambience ambience_camp_center_day fadein 5
    scene bg ext_houses_day with blind_l
    "Мы уже очень долгое время молчали."
    "Я до сих пор пребывал в несколько смешанных чувствах из-за открывшихся обстоятельств с моим омоложением."
    "А Лена, наверное, просто в силу природной скромности."
    "Как я понял, хаотичное на первый взгляд нагромождение домиков подчинялось строгим законам геометрии — при известном желании можно было проследить даже своего рода «улицы»."
    "Хотя я, как человек, большую часть своей жизни отдыхавший летом в детских оздоровительных лагерях, знал, что настоящий старожил всегда плюёт на оригинальную планировку."
    "У любого ветерана есть свои хоженые маршруты и тропинки, чаще всего натоптанные сотнями единомышленников через кусты и лес."
    "Лена же старательно держалась бетонной дорожки, сворачивая лишь там, где это было предусмотрено строителями."
    am "Ты здесь впервые?"
    "Спросил я."
    "Пока мы шли вот так, бок о бок, было немного полегче общаться, чем глаза в глаза."
    scene bg ext_houses_day at zenterleft
    show un normal pioneer at cleft
    with dissolve
    un "В прошлом году… Была."
    "Ответила она, всё так же старательно глядя себе под ноги."
    am "Нравится в лагере?"
    "В конце концов, надо начинать здесь осваиваться, понять, куда я попал и что мне дальше делать."
    "Стеснительная девушка показалась мне идеальной кандидатурой, так как не станет задавать неудобных вопросов."
    un "Здесь тихо и спокойно."
    "Ответила она."
    un "Дети шумят, но они же дети…"
    "Она хотела было что-то сказать, но её перебил неожиданно подошедший мальчик лет одиннадцати:"
    show al normal pioneer at right
    with dissolve
    al "Эй, я тебя тут раньше не видел. Ты новичок?"
    "От его тона веяло привычкой к тому, что все вокруг подчиняются."
    "Не знаю, почему меня это так задело."
    am "А тебе-то какое дело?!"
    show al smile pioneer with dspr
    al "Да никакого."
    "Пожал плечами он, отступая."
    ml "Просто просили передать это тебе."
    "На протянутой ладони лежал…"
    "Мой телефон."
    am "Спасибо, это я за…"
    stop music fadeout 3
    "Я не договорил."
    play sound sfx_wind_gust
    "Мимо нас промелькнул едва различимый силуэт, и я вдруг обнаружил, что стою с пустой рукой."
    hide un with pixellate
    "Да ещё и в одиночестве!"
    "Лена, не говоря дурного слова, кинулась в погоню!"
    menu:
        "За ними!":
            play music music_7dl["anglegrinder"] fadein 3
            $ alt_day1_me_d3_chase = True
            $ lp_un += 1
            th "Поймаю — закопаю."
            "Мрачно пообещал себе я."
            "Сегодня надо мной целый день все издеваются, бьют, орут и доводят до обморока."
            "А мне даже не на ком злость сорвать."
            "Но здесь и сейчас…"
            "У меня будет полное право долго и остервенело пинать воришку."
            scene bg ext_houses_day at fast_running
            with flash
            "А тот, видимо, почуяв мои самые неблагие намерения, с душой так поднажал."
            am "Врёшь, тварь!"
            "Сквозь стиснутые зубы прорычал я, ускоряясь."
            "Всем известно, что рекорд на сто метров в девять с половиной секунд принадлежит ямайцу Усэйну Болту, моему сверстнику."
            "Но мало кто знает, что всю дистанцию он покрыл за 47 шагов, таким образом, длина его шага составляла 2,2 метра."
            "Труднодостижимый для обычного человека результат."
            "Но есть одна интересная техника."
            "В старом «квейке» она называлась…"
            "Распрыжкой."
            show ftl_anim with Shake((0, 0, 0, 0), 4.0, dist=10)
            "Я поймаю его и утрамбую в бетон, чтобы наружу торчали одни только уши."
            "Надо просто быть ещё чуточку быстрее…"
            show ftl_anim with Shake((0, 0, 0, 0), 4.0, dist=15)
            "Ещё…"
            show ftl_anim with Shake((0, 0, 0, 0), 4.0, dist=20)
            "Чуточку…"
            show ftl_anim with Shake((0, 0, 0, 0), 4.0, dist=15)
            "Быстрее…"
            "Икроножные мышцы заныли, когда я, оставляя за собой инверсионный след, вышел на первую космическую."
            "За спиной осталась удивлённо вскрикнувшая Лена, которую я обогнал как стоячую."
            am "Быстрее…"
            "Спина воришки стала увеличиваться в размерах — я догонял его."
            "Ещё немного, и… Ударю чуть выше колена, чтобы гарантированно клюнул носом в землю."
            scene bg ext_square_day:
                xalign 0.5 yalign 0.5 zoom 1.25
                linear 0.3 zoom 1.0 xalign 0.5 yalign 0.5
            show blackout_exh
            show ftl_anim with Shake((0, 0, 0, 0), 4.0, dist=15)
            with flash
            voice "Эй, лови!"
            "В воздух взлетело что-то."
            "Что-то блеснувшее на солнце."
            "Чёрное, но с одной отражающей стороной!"
            scene bg ext_square_day
            show blackout_exh2
            with fade
            th "Телефон!"
            play sound sfx_jump_into_hole_2
            "И…"
            th "Поймал!"
            "В прыжке, сделавшем честь иному голкиперу, я поймал ценный груз в воздухе."
            play sound sfx_7dl["breath"] fadein 3
            stop music fadeout 3
            th "Умён, гад."
            "Я сидел на корточках, пытаясь прийти в себя — спурт навстречу бронзовому истукану вытянул из меня остатки сил."
            th "Гадский воришка, ну погоди у меня!"
            "Я погрозил кулаком сам не зная кому."
            "Воришка скрылся…"
            "Я сжимал многострадальный смарт в потной ладони — миссия была выполнена."
            play ambience ambience_camp_center_evening fadein 3
            "Сейчас бы отойти в сторонку, лечь и сдохнуть."
            "Кажется, физические нагрузки моему новому телу тоже не очень по нраву, не слишком-то оно тренированное."
            "Ну, а Лена, только-только догнавшая меня, вообще выглядела так, будто сейчас упадёт."
            "Она прислонилась к постаменту и запалённо дышала."
            "А я, как это обычно и бывает в такой ситуации, воспринимал информацию отстранённо, не пытаясь её понять."
            "Взгляд скользил по буквам."
            th "Г-Е-Н-Д-А. {w}Чья-то партийная кличка? Как у Кобы?"
            show blackout_exh3
            with fade
            "Ещё несколько минут, заполненных одним только дыханием и желанием сплюнуть."
            hide blackout_exh3 with dspr
            "Наконец, темнота перед глазами немного отступила."
            "И я совершенно естественно спросил:"
            am "У нашего вора и правда были кошачьи ушки?"
            scene bg ext_square_day at zentercenter
            show un normal pioneer
            with dissolve
            "Лена пожала плечами."
            un "Я не знаю, у меня не очень хорошее зрение."
            "Небольшая пробежка сплотила нас так, что больше не приходилось тянуть из девушки слова клещами."
            un "Но в лагере ходит история про кошкодевочку, которая ворует еду и всякие мелочи."
            am "Вроде страшилки?"
            "Лена кивнула."
            un "Пойдём к вожатой уже?"
            "Она окончательно пришла в себя."
            "Я кивнул и последовал за ней."
        "А… Что?":
            $ lp_mi += 1
            play music music_7dl["unforgotten"] fadein 3
            "Пока я глазами хлопал, вора и след простыл."
            "И Лены тоже."
            show al laugh pioneer with dspr
            ml "Раззява!"
            hide al with dissolve
            "Презрительно кинул пионер и, развернувшись, ушёл."
            "Но меня это заботило меньше всего."
            am "Телефоооон!"
            "Горестно возопил я."
            th "О мой замечательный чайнафон с кучей ненужных функций и неубиваемостью уровня Нокии 3310!"
            th "На кого ж ты меня покинууууул!"
            th "И где мне теперь искать его?"
            th "Его же сто процентов этим же вечером в ближайшей деревне продадут за бутылку водки!"
            "В последнее верилось с трудом, но я привык рассчитывать на худшее."
            "И провожатую потерял."
            "Что за день такой сегодня… Глупый."
            scene
            $ renpy.show("bg ext_houses_day", what = Desat1("bg ext_houses_day"))
            with fade
            "Хуже уже не будет, сильнее, чем собралась, ругать меня вожатая не станет."
            th "Не убьёт же она меня?"
            "Постояв немного на месте и повздыхав, я направился куда глаза глядят."
            "А глядели они прямо."
            "Четыре бочки с одной стороны, три бочки и поворот с другой."
            "Улица моего позора."
            "Бульвар, который потомки назовут с французским прононсом: Бульв{b}а{/b}р Обоср{b}а{/b}мс."
            am "Очень хочется ругаться матом, не смотрите на меня!"
            "Зарычал я на сбившуюся неподалёку стайку девочек лет двенадцати-четырнадцати."
            "Те с визгом брызнули в стороны."
            "Улица закончилась, плавно нисходя до обыкновенной тропинки между деревьями."
            am "Вот точно, уйду в лес, и пусть меня там съедят волки."
            am "Хотя бы что-то в жизни своей сделаю правильно."
            "Но это же просто огороженная территория — какой бы запущенной она ни выглядела."
            "Какие уж тут волки."
            "Но мне нужно было найти выход острому чувству жалости к себе."
            "И оплакать безвременную кончину смартфона."
            play sound sfx_hiding_in_bush fadein 0
            scene bg ext_path_day with dissolve
            play ambience ambience_forest_day fadein 3
            "Я оказался в роще."
            "Здесь было тихо и полутемно."
            th "Вот поселюсь здесь и буду жить."
            th "Построю шалаш на дереве и…"
            dreamgirl "…назло мамке уши отморожу."
            th "Похоже, придётся к Виоле прогуляться ещё и за зелёной таблеткой."
            dreamgirl "Не боись, их ещё не изобрели."
            th "Я и не боюсь. Это тебе надо бояться."
            "Больше не слушая причитаний в голове, я отправился по тропинке дальше."
            scene bg ext_musclub_day with fade
            play ambience ambience_camp_center_day
            "И, кажется, я и правда ошибся с направлением."
            "Пробравшись между деревьями, я вышел на открытое пространство, над которым доминировало одно большое здание."
            "Больше всего оно походило на самый обыкновенный дом, у которого зачем-то сняли одну стену, обрезали пополам по высоте остальные, а те, что остались, застеклили как только могли."
            "Получилась любопытная конструкция, на которую было приятно посмотреть."
            "А так как поблизости не было никаких других построек, я сделал вывод, что это либо репетиционная база, либо библиотека."
            th "Наверное, всё же база — книги не очень любят прямой солнечный свет."
            "Уши уловили гитарные переборы, и я подошёл поближе."
            "Гитара доносилась из-за двери, я потянул было руку открыть её."
            play music music_7dl["ourfirstmet"] fadein 2
            scene bg ext_musclub_day at zentercenter
            show mi shocked pioneer
            with flash
            with vpunch
            "Дверь распахнулась, едва я успел взяться за ручку."
            mi "Ой!"
            "Дёрнуло довольно сильно.{w} Для девочки, я имею в виду."
            "И это снова была она!{w} Та самая девушка из медпункта!"
            show mi sad pioneer with dspr
            am "Привет, как лоб?"
            "Не нашёл ничего умнее спросить я."
            if alt_day1_me_d3_sl_conv:
                "Мы видимся уже в третий раз, а так толком и не познакомились."
            else:
                "Здорово, когда видишь человека второй раз в жизни — а у вас уже общие воспоминания!"
            mi "Ой, здравствуйте ещё раз! А лоб у меня в порядке, папа говорит — там сплошная кость, так что болеть нечему, всё заживает быстро! Разве что должна быть шишка, если сильно стукнулся, но мы же не сильно стукнулись? Я хочу сказать, мне было почти даже и не больно! Вот."
            if alt_day1_me_d3_sl_conv:
                mi "А вы разве не должны быть у вожатой? Вас же Славичка хотела отвести, но ей было некогда, она к вам Леночку отправила. Разминулись?"
                am "Не совсем."
                "Я не стану объяснять ей разные сюжетные моменты, хорошо?"
                am "Так или иначе, я не дошёл до вожатой, поэтому…"
                mi "Да? Странно, я всегда думала, что Леночка очень ответственная девочка, ей можно доверить — ну, всякое! А уж тем более проводить по лагерю нового человека."
            else:
                am "Я в порядке. Даже форму дали новую."
                show mi happy pioneer with dspr
                mi "Ой, как она вам идёёёт!"
                mi "А мне больше наши сейлорфуку нравятся, они такие удобные, и ткань хорошая, носишь и не замечаешь! Нет, я не хочу сказать, что пионерская форма плохая, просто мне непривычно, я всю жизнь носила другую одежду, так что сразу вдруг не могу привыкнуть."
            "Та-ра-тор-ка."
            "Кажется, у этой девушки дефицит внимания."
            "Она сидит одна на отшибе здесь, потому что никто не хочет слушать её пулемётные очереди."
            show mi smile pioneer with dspr
            mi "Но это ничего, я вас тогда провожу сама, вы не против?"
            show mi upset pioneer with dspr
            mi "Только не подумайте, пожалуйста, что я навязываюсь, если вы не хотите, то вы можете отказаться, и я не обижусь. Правда-правда!"
            am "Да нет…"
            "Улыбнулся я. {w}Несмотря на все странности девушки, она была очень славная."
            th "Разве что если бы её было чуточку поменьше, я бы оценил. Правда-правда!"
            am "Пожалуйста. {w}Но почему ты меня на «вы» называешь? Я же практически…"
            "Визуально, во всяком случае."
            am "Твой ровесник."
            show mi smile pioneer with dspr
            mi "Простите, но я так воспитана, я стараюсь уважать любого человека, когда его встречаю!"
            mi "Мне было очень тяжело научиться разговаривать по-русски без хонорификов… Эээ… Суффиксов! Но если вы мне незнакомый, то я к вам обращаюсь на «вы», как к незнакомому человеку обращалась бы с суффиксом «сан», понимаете?"
            th "Что-то такое я слышал, когда смотрел дома всякие мультики. Все эти «саны», «чаны».."
            am "Мы теперь знакомы, теперь ты можешь называть меня на «ты»."
            show mi grin pioneer with dspr
            mi "Вообще-то, нас так и не представили друг другу, и я не знаю вашего… твоего имени."
            am "О, прости."
            am "Моё имя…"
            stop music fadeout 3
            "Я не закончил — то самое шестое чувство, интуиция, сигнализировало об опасности!"
            "Я поднял глаза."
            show us laugh sport far at left behind mi with flash
            "Со стороны входа на террасу стояла невысокая девочка с копной красных волос, стянутых двумя хвостами, а у ног неё лежал мяч."
            play music music_list["doomed_to_be_defeated"] fadein 3
            us "Пенальти!"
            play sound sfx_soccer_ball_kick
            "Она пнула мяч, и тот с огромной скоростью полетел в нашу сторону."
            am "А… Что?"
            "Я смотрел на приближающийся мяч, и ничего не мог сделать."
            "Я просто не успевал."
            "Как в какой-то компьютерной игре, перед глазами наяву встала колонка цифр баллистического расчёта с траекторией мяча, упирающейся прямиком в циановую головку."
            "Я заступил на путь следования и зажмурился."
            with flash
            "Ба-бах!" with vpunch
            "Мяч попал мне в голову."
            show mi scared pioneer
            show blackout_exh
            with dissolve
            "В мою бедную, несчастную голову!"
            "О всеблагой рандом, пусть уже кто-нибудь отпилит её и спрячет в сейф — мне надоело, что по ней сегодня все бьют!"
            "Перед глазами всё потемнело, а рыжая бандитка, прокричав что-то ободряющее, скрылась с места преступления в неизвестном направлении, забрав с собой орудие преступления."
            hide us
            show mi scared pioneer behind blackout_exh2 with dspr
            mi "Ой, с вами всё в порядке?"
            am "С тобой."
            "Бросил я."
            "Разумеется, со мной не всё в порядке, мне, блин, мячиком по голове зарядили — как со мной может быть что-то в порядке?!"
            stop music fadeout 3
            am "Давай ты просто отведёшь меня к вожатой, и всё?"
            "Девушка молча кивнула."
            "Видимо, здесь не каждый день происходит расстрел мячиками заезжих пионеров."
    scene bg ext_house_of_mt_day with fade
    "Этот домик отличался от прочих в первую очередь формой."
    "Он представлял собой двускатную крышу, под которой, будто на чердаке, и должны были жить пионеры."
    "Рядом с крыльцом замер добротный советский велосипед, а по другую руку ждали своего часа два деревянных шезлонга."
    am "Домик вожатой?"
    if alt_day1_me_d3_chase:
        show un normal pioneer with dissolve
        hide un with dissolve
    else:
        show mi normal pioneer with dissolve
        hide mi with dissolve
    "Быстро кивнув мне, провожатая ретировалась, оставляя меня в одиночестве."
    "Надеюсь, в этот раз это не будет очередным розыгрышем."
    "Хотя это же не Алиса, она вряд ли станет меня подставлять."
    "Ведь правда?"
    play sound sfx_knock_door2
    "Я постучал."
    mt "Войдите!"
    "Раздался женский голос изнутри."
    "И я вошёл."
    "Что мне ещё оставалось?"
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_O:
    play sound sfx_open_door_clubs_2
    scene bg int_house_of_mt_day with dissolve
    play ambience ambience_int_cabin_day fadein 3
    play music music_list["smooth_machine"] fadein 5
    "Внутри «чердак» оказался достаточно уютным и милым."
    "Места, конечно, было не очень много, но с другой стороны, и не предполагалось, что отдыхающие будут проводить много времени под крышей."
    "Проснулся, оделся, умылся, собрался и поскакал."
    "Но вожатая, как и Славя, тоже, судя по всему, была большой любительницей придавать помещению жилой вид."
    "Её вещи расползлись по всей комнате, занимая поверхности, рамы и вешалки."
    "Даже вторая кровать, судя по идеальной застеленности, ничья, была частично оккупирована вещами вожатой."
    "Сама же вожатая — очень красивая девушка лет на пятнадцать младше меня прежнего — стояла рядом со столом и ждала, пока я обращу на неё внимание."
    "И очень знакомая!"
    $ meet('mt', 'Ольга')
    scene
    $ renpy.show("bg int_house_of_mt_day", at_list = [zenterright], what = Noon("bg int_house_of_mt_day"))
    show mt normal pioneer at cright
    with dissolve
    show name_mt with flash:
        pos(460,350)
        linear 1.0 pos (260,330)
    show mt smile pioneer at cright with dissolve
    pause(2)
    scene bg int_house_of_mt_day
    show mt smile pioneer
    with dissolve
    mt "Ну, здравствуй."
    "Чуточку ворчливо начала она."
    mt "Я тебя уж заждалась, думала сама за тобой идти."
    am "Простите?"
    mt "Не прощу."
    show mt smile pioneer with dspr
    mt "Автобус приехал в три часа, а сейчас половина восьмого, расскажешь мне, где ты лазил всё это время?"
    am "Э… Ну…"
    show mt laugh pioneer with dspr
    mt "Попался! {w}Да шучу я, шучу."
    "Смех у неё был мелодичным и приятным."
    "Совсем не такой, какой у неё был (или будет?) позже."
    "Наверное, сейчас она пользуется успехом у противоположного пола."
    "Хотя, если здесь все люди такие же красивые, как встречные мной пионерки и Ольга, неизбежна некая конкуренция."
    "Но это всё лирика, надо о главном."
    am "Послушайте, произошла какая-то ошибка…"
    "Начал я, чувствуя себя героем всё того же дешёвого ситкома."
    show mt normal pioneer with dspr
    mt "О, правда?"
    am "Я не помню, чтобы вы меня нанимали, как я вообще здесь оказался, а?"
    mt "Нанимала?"
    "Ольга выгнула бровь и с безжалостным любопытством посмотрела на меня."
    "Тут до меня дошло, что выгляжу я сейчас малость не на свои годы."
    "И в целом не слишком-то на себя похожу, но…"
    mt "Я не помню, чтобы кого-то вообще нанимала. {w}А вот ты приехал по путёвке…"
    show mt grin pioneer with dspr
    mt "Как всегда просроченно."
    th "По путёвке…"
    stop music fadeout 3
    with fade
    th "Неужели это значит, что у меня…"
    am "Получилось?"
    play music music_7dl["wheres_wonderland"] fadein 3
    show mt sad pioneer with dspr
    mt "Нет. Иначе бы ты меня узнал."
    mt "Охохо, и за что мне несчастье такое."
    "Вздохнула вожатая."
    "Она села на стул со стороны кровати и кивком указала мне на другой."
    "Видимо, с этого момента начинается серьёзный разговор."
    mt "Как тебя зовут?"
    show mt smile pioneer with dspr
    mt "Или ты и в этот раз безымянный посетитель?"
    "Улыбнулась она."
    th "И в этот раз?.."
    mt "У меня по путёвке ты значишься как Семён. {w}А на самом деле?"
    "Я пожал плечами."
    am "Можно и Семёном."
    "От имени нигде не звенел колокольчик, но у меня сейчас он в принципе ни от чего не звенел."
    mt "А как лучше всего?"
    am "Да откуда же мне…"
    mt "Неужели ты не помнишь?.."
    "Вожатая понизила голос:"
    mt "Амнезия до утраты собственного имени — это, знаешь ли…"
    mt "Придётся вернуть тебя доктору Виоле."
    "Напугала кота сосиской."
    mt "И всё же, как твоё имя?"
    "Она подтолкнула мне бумагу с официальным штемпелем, на которой значилось: «Состав первого отряда», и ткнула пальцем на седьмую строчку."
    "Там стояла незнакомая мне фамилия с инициалами."
    am "Персунов Эс Эс."
    "Послушно прочитал я."
    am "Да, можете звать меня Семёном, если хотите."
    am "Но лично я называю себя…"
    am "Дём."
    show mt surprise pioneer with dspr
    mt "Какое любопытное имя. {w}Как самочувствие, Дём?"
    "Не хватало, не хватало второй таблеточки."
    "Слишком много узнаваемых людей вокруг, очевидно, это психоз."
    "Доктор-доктор, дайте мне таблетку…"
    am "Может быть, я вернусь в медпункт?"
    show mt smile pioneer with dspr
    mt "Нет уж! Никаких таблеток по купированию памяти. Ты должен вспомнить всё."
    mt "Понял меня?"
    "Она достала с полки ежедневник, а оттуда — сложенный пополам лист."
    mt "Держи. Читай."
    stop music fadeout 3
    "Я растерянно принял листок и развернул его."
    $ set_mode_nvl()
    "Привет! Прииииивет!"
    play music music_7dl["let_me_down"] fadein 3
    "Любопытно, как тебя зовут в этот раз?"
    "Ох, святой рандом, сколько разных имён, ты не устаёшь удивлять, отыскивая себе новые."
    "Знаешь, старайся не особо кричать окружающим о том, что и как с тобой произошло."
    "Потому что я-то знаю. А вот если узнают они…"
    "Ты никогда не был в лаборатории? А в качестве подопытной крысы?"
    "Короче говоря, язычок на замочек и ни-ни никому."
    nvl clear
    "Теперь о том, что тебе следует знать."
    "Во-первых, здесь всё ещё совок. Да, великий советский Совок конца восьмидесятых."
    "Понятия не имею, как это работает, у меня ушло несколько лет на то, чтобы просто стать частью этого маленького мирка."
    "Во-вторых, это не наш дом."
    "Пионерия тут гуляет до восемнадцати, кое-какая разница в технологиях, законах."
    "История тоже несколько отличается."
    "Ну да ты видел этого бронзового бармалея на площади в очках, сам понимать должен."
    "В-третьих, ты здесь не впервые."
    "Точнее, это не совсем ты, а просто человек, здорово на тебя похожий, и вас связывает кое-какой невыплаченный долг."
    "Короче, не облажайся и не подведи."
    nvl clear
    "Ольке можешь верить, но без фанатизма, она всё-таки просто человек."
    "Которого жутко бесит то, что ты её не узнаёшь."
    "Так что сошлись на провалы в памяти или плохое самочувствие, ну ты сам знаешь."
    "И постарайся не изображать из себя буку, как ты любишь — здесь это не работает."
    "Пожалуйста."
    "Засим адью с неприличными жестами."
    "Сэм."
    $ set_mode_adv()
    "Я вернул письмо вожатой."
    $ meet('mt', 'Ольга')
    mt "Ну и?"
    am "Ольга…"
    "Она несколько секунд вглядывалась мне в глаза, видимо, пытаясь там что-то найти."
    "Не нашла."
    show mt sad pioneer with dspr
    "Вздохнула."
    mt "…Дмитриевна."
    $ meet('mt', 'Ольга Дмитриевна')
    am "Ясно, значит, вы вожатая, а я пионер."
    show mt smile pioneer with dspr
    mt "Вроде того. И если тебя это интересует, письмо я не читала."
    mt "Никогда."
    "Неудивительно."
    "У меня всегда был почерк как кура лапой."
    "Его я сам не всегда разбирал со словарём, что уж говорить про кого-то другого."
    "Верно, письмо действительно было написано моим почерком."
    "Кажется, «в-третьих» из письма соответствовало истине."
    "Что ж. Значит, буду наслаждаться отдыхом на халяву."
    "Почти как бесплатная путёвка в Дубаи, только с омоложением всего организма в качестве бонусного сервиса."
    am "Ясно, Ольга…"
    menu:
        "…Дмитриевна":
            $ reput += 10
            "Я с усилием выговорил её отчество."
            "За те пять лет, что я проработал под её началом, мы хоть и не перешли на «ты», но называли друг друга только по имени."
            "И отчего то в голове было странное, звенящее чувство, словно мы знакомы гораздо, гораздо дольше."
            "Были друг для друга кем-то большим, чем начальник и подчинённый."
            "Но раз у меня уже появился какой-то там долг…"
            "Попробуем пока поиграть по правилам."
        "…":
            "Я выдержал паузу."
            show mt sad pioneer with dspr
            mt "Ольга Дмитриевна, пионер."
            mt "Я твоя вожатая. {w}Потрудись соблюдать субординацию."
            th "Жаль. Не сделают мне поблажек."
    am "Так что мне делать дальше?"
    show mt normal pioneer with dspr
    mt "Сегодня уже ничего не успеешь, слишком долго гулял."
    "Она посмотрела на часы."
    mt "Пока ходи, привыкай к лагерю, а завтра с утра начнём тобой заниматься."
    mt "Сейчас ужинать пойдём, время восемь."
    mt "Знаешь, где столовая?"
    "Я положительно кивнул головой."
    mt "Отлично. Можешь идти, подходи туда через десять минут."
    mt "Или, если хочешь, подожди чуток и пойдём вместе."
    am "Подожду."
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_supper:
    play music music_list["my_daily_life"] fadein 1
    scene bg ext_dining_hall_away_sunset with dissolve
    "Столовая оказалась ровно той, какой была в моё время."
    "Всё то же приземистое одноэтажное здание с крыльцом и двойной дверью, обеспечивающей достойную пропускную способность."
    "Разве что оконные рамы все ещё деревянные."
    scene bg ext_dining_hall_near_sunset at zentercenter
    show us smile sport far at left with moveinleft
    pause(0.5)
    scene bg ext_dining_hall_near_sunset at enterright
    show us smile sport far at left
    show mt angry pioneer with moveinright
    pause(2)
    show us dontlike sport at fleft with moveinright
    if not alt_day1_me_d3_chase:
        "Мимо нас хотела прошмыгнуть давешняя хулиганка с мячом"
    else:
        "Перед нами пыталась просочиться какая-то мелкая пионерка"
    extend ", но вожатая была начеку и сгребла ту в воздухе."
    mt "Стоять!"
    mt "Марш руки мыть!"
    show us normal sport at fleft with dissolve
    us "Ну Ольга Дмитриевна!"
    mt "Ничего не знаю, но с грязными руками я тебя в столовую не пущу!"
    us "Ладно, ладно. Иду."
    hide us with dissolve
    "Я, с огромным удовольствием наблюдавший всю сцену, даже вздрогнул, когда вожатая окликнула меня."
    mt "Ты почему стоишь?"
    am "Э…"
    show mt laugh pioneer with dspr
    mt "Что «э», беги за ней, узнаешь, где рукомойник!"
    am "Понял."
    "Не теряя больше ни секунды, а поспешил за пионеркой в футболке."
    scene bg ext_washstand2_day with fade
    stop ambience fadeout 3
    play sound sfx_open_water_sink
    pause(1)
    play sound_loop sfx_water_sink_stream
    us "Ах, мне жарко, я сейчас в обмырок упаду…"
    "Неласково пробормотали сзади."
    th "Вот оно, да?"
    th "Первая проверка на прочность?"
    "Шутка юмора с тазиком была самодеятельностью, а теперь тут дела посерьёзнее?"
    "А сейчас определяется моё место в иерархии, я так понимаю."
    stop sound_loop
    play sound sfx_close_water_sink
    pause(1)
    scene bg ext_washstand_day
    "Я неторопливо выключил воду и обернулся."
    "И…"
    scene bg ext_washstand_day:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 0.3 zoom 1.1 xalign 0.5 yalign 0.45
    th "Никого?"
    "Хм…"
    play music music_list["i_want_to_play"] fadein 5
    show us laugh sport with moveinbottom
    us "Новичок-новичок, косоногий казачок."
    scene
    $ renpy.show("bg ext_washstand_day", at_list = [zentercenter], what = Desat("bg ext_washstand_day"))
    show us normal sport
    with dissolve
    show name_us:
        pos(660,180)
    show us smile sport
    with flash
    pause(2)
    scene bg ext_washstand_day
    show us laugh sport
    with dissolve
    "Оттарабанила она."
    am "Эй! Перестань меня обзывать!"
    am "Как тебя зовут, кстати?"
    show us grin sport with dspr
    us "Хватит обзывать, хватит обзывать!"
    "Она ткнула в меня пальцем и показала язык."
    us "Новичок и есть!"
    hide us with easeoutbottom
    "И унеслась прочь."
    "Даже имени не сказала. {w}Мелкая пакость."
    us "Уля!"
    "Донеслось от столовой."
    stop music fadeout 3
    us "Моё имя Уля!"
    $ meet('us', 'Уля')
    scene bg ext_dining_hall_near_sunset with dissolve
    "Спустя пять минут я, отряхивая ладони от холодной — ледяной! — воды, поднялся на крылечко и вместе с вожатой зашёл внутрь."
    play sound sfx_open_door_clubs fadein 0
    scene bg int_dining_hall_people_day
    with dissolve
    play ambience ambience_dining_hall_full fadein 3
    "Внутреннее убранство местного храма чревоугодия не удивило меня ничем."
    "Длинные столы на двенадцать человек, выстроившиеся у стен, столы чуть короче по другую сторону прохода, чтобы обслуживающий персонал с тележками мог подобраться и сервировать или убрать со стола."
    "Кафель на полу и стенах, стулья из нержавеющей стали и колонки под потолком на случай, если на улице будет дождь."
    "Наособицу стояли небольшие столики для обслуживающего персонала, также полностью занятые."
    "Ощутив на себе взгляд, я кивнул добрейшему доктору Виолетте."
    "Она улыбнулась мне, а вот быковатый мужик рядом с ней нахмурился, одаряя меня оценивающим взглядом."
    th "Ух ты, а она, оказывается, несвободна!"
    "Если только этот спортивный мальчик не из числа неправедно отвергнутых."
    th "Судя по тому, что она на него даже не смотрит…"
    dreamgirl "А у тебя работает интуиция!"
    th "Заткнись."
    "Меня это не убедило, но спорить я не стал, и без того завис на проходе, раза три в плечо толкнули."
    "Место по другую руку от Виолы занимала девушка из медпункта."
    if not alt_day1_me_d3_chase:
        "Она же девушка из музыкального клуба."
    "А что самое забавное, мы так и не сумели познакомиться за сегодня!"
    "Завтра обязательно наверстаю."
    th "Забавная она, хоть и болтушка."
    show mt normal pioneer at right with dissolve
    mt "Так, сейчас тебе место найдём."
    "Вожатая подвела меня к паре столов, которые, судя по всему, принадлежали моему отряду и, немного подумав, указала пальцем на свободное место рядом с мелкой."
    mt "Давай пока здесь, а там посмотрим.{w=0.5}{nw}"
    show dv normal pioneer2 with dissolve
    show mt angry pioneer with ease
    extend " Двачевская, стоять!" with vpunch
    show dv surprise pioneer2 with dspr
    dv "Чего ещё?"
    mt "Ты во что одета?"
    dv "В форму?"
    "Разумеется, в форму. Вопрос лишь в том, как именно была надета эта форма."
    "Если рубашка на голое тело, повязанная ковбойским узлом и галстук в качестве напульсника — это форма…"
    "Правда, не пионера, а кое-кого другого."
    "И похоже, определение рвалось с языка не у меня одного."
    "Ладно, каждый сходит с ума в меру способностей."
    "Так что вожатая развернула Алису в сторону выхода:"
    mt "Приведёшь себя в порядок, тогда возвращайся, а пока марш отсюда."
    hide dv with dissolve
    "Пробормотав нечто непристойное, Алиса испарилась."
    am "Ольга Дмитриевна, отсадите меня от мелкой, она гадость задумала."
    "Сделал заход я."
    mt "Прости, но мест нет. {w}Сам не видишь?"
    am "Угу. Если я останусь без ужина, это будет на вашей совести."
    show mt smile pioneer with dspr
    mt "Я постараюсь справиться с этим и жить, как раньше."
    "Усмехнулась вожатая, поощрительно кивнула уже переодевшейся Алисе и пошла ужинать."
    hide mt with dissolve
    "Кроме уже знакомой мне девушки из медпункта (в отсутствие имени стану звать её так) и Ульяны с Алисой, были здесь и другие знакомые лица:"
    show sl smile pioneer with dspr
    "Славя — человек, который был настолько добр ко мне, что выделил мне форму, собственную расчёску и даже уложил спать, когда мне совсем поплохело."
    "Надо бы держаться к ней поближе, мне кажется, на неё можно положиться."
    "Славя одарила меня быстрой улыбкой и вернулась к ужину."
    hide sl
    show un shy pioneer
    with dissolve
    "По соседству с ней сидела моя вторая провожатая — Лена."
    "Она не перестала стесняться даже сейчас."
    "Вон как покраснела, поймав мой взгляд."
    if alt_day1_me_d3_chase:
        "А мне показалось, что мы прошли этот этап…"
    hide un
    show el normal pioneer at left
    show sh normal pioneer at right
    with dspr
    "И замыкали ряд два мальчика."
    "Это хорошо, что здесь есть пацаны, надо будет подойти к ним наладить контакт."
    "Впрочем, их место говорило само за себя: в отряде царит жёсткий матриархат."
    us "Эй, псст!"
    scene bg int_dining_hall_people_day at zentercenter
    show us smile sport
    with dissolve
    am "Что?"
    "Не было печали…"
    us "Где твоя котлета?"
    am "Моя… Что?"
    "Я перевёл взгляд на тарелку."
    "Всё верно, пока я ротозейничал да оглядывался, кто-то покрал самую важную часть ужина!"
    "И не надо быть Эйнштейном, чтобы понять, чьих рук это дело."
    $ meet('us', 'Ульяна')
    am "Котлету."
    us "В большой семье клювом не щёлкают."
    am "Ах ты маленькая…"
    menu:
        "Сама отдашь?":
            $ reput += 10
            $ lp_us += 1
            "Я подвинул ей тарелку и поощряюще кивнул."
            am "Давай. В темпе вальса, котлету на бочку и всё такое."
            us "Да нет у меня её, сам смотри!"
            "Я заглянул к ней в тарелку."
            am "Быстра же ты жрать, пани…"
            "Пробормотал я."
            "Делать нечего, пришлось есть пюре — в моей тарелке, и отобранное у Ульяны."
        "Смириться":
            am "Надеюсь, ты в курсе, что воры чужих котлет склонны к ожирению и волосатым ладоням."
            show us dontlike sport with dspr
            us "Эй, у меня не волосатые ладони!"
            am "Да я догадался уж."
            "Добавки на раздаче не дали, ещё и обматерили."
            "Ну его в баню."
            "Встал из-за стола голодным, куда это годится."
        "Подавись":
            $ reput -= 30
            extend " дрянь!"
            with vpunch
            show us dontlike sport with dspr
            "Я не был совсем уж идиотом, все эти правила с проверкой новичков на прочность знал прекрасно."
            "Ни разу не был в тюрьме, чтобы сравнивать, но, наверное, есть нечто похожее тут и там: как ты себя поставил, это главное."
            "Поэтому вполне было логично, что я использовал свою секретную атаку:"
            "Я взял тарелку с пюре и надел её на рыжую голову."
            "А потом долго-долго колотил по тарелке ложкой, терзая барабанные перепонки окружающих акустической атакой."
            "И наконец, схватив её за обе «ракеты», как следует повозил носом по пюре!"
            "Ну разве не волнительная картина?"
            with fade
            th "И почему я не ренегат из одной известной игры? Я бы ещё и смехом как у доктора Зло заливался, творя бесчинства и непотребства."
            th "Антикризисная программа для неудачника."
            th "Мечты-мечты."
            "С трудом подавив в себе желание воплотить волнительную картину в жизнь, я поднялся."
            am "Приятно подавиться."
            us "Эй, ты куда?"
            "Крикнула Улька мне в спину."
            am "Не твоё дело."
            "Плакал мой ужин."
            "Эх."
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_U:
    scene bg ext_dining_hall_near_sunset with dissolve
    play ambience ambience_camp_center_evening
    "Гадская Улька, чтобы ей ночью лук приснился, оставила меня без самой важной части ужина."
    "Не знаю, я, наверное, буду мстить."
    "И мстя моя будет коварчива и страшна!"
    "Вообще день был что-то не очень."
    "Очень не очень — начиная с пробуждения лоб в лоб с той болтушкой-хохотушкой."
    if not alt_day1_me_d3_chase:
        "Ещё и телефон подрезали, а пока я стоял, рот раззявив, утащили."
    "Так что я не удивился завершению этого дня таким вот образом."
    "Если уж не везёт, то не везёт во всём."
    "Меня вдруг осенило:"
    th "Ну да! {w}Я же опять ту гадину в коротком платьице видел во сне!"
    th "Значит, впереди ожидает как минимум неделя несчастий."
    "Стоило бы ожидать. {w}Одно только попадание сюда чего стоит."
    "Сзади раздался смешок, и я обернулся."
    scene bg ext_dining_hall_near_sunset at zenterleft
    show us smile sport at cleft
    with dissolve
    play music music_list["i_want_to_play"] fadein 5
    us "Хы!"
    am "Опять ты. Отстань от меня уже!"
    us "А давай столовую ограбим и тебя накормим?"
    am "Что?"
    us "Конфеты! Халвичные, содержащие все полезные витамины и минералы для растущего детского организма!"
    "С трудом выговорила она."
    am "Я с тобой никуда не пойду."
    "Отвернулся я."
    show us laugh2 sport with dspr
    us "Ну пожалуйста-пожалуйста!"
    "Запричитала она."
    am "Нет."
    "Отрезал я."
    "Проходящие мимо пионеры из других отрядов окидывали странными взглядами наш трогательный тандем, но мне не было дела до них."
    "Всё, чего я хотел — это чтобы меня оставили в покое."
    "И сейчас лучше немного потрудиться и побыть немного резким — это в будущем сэкономит кучу нервных клеток."
    us "Да?"
    show us sad sport with dspr
    "Девочка вздохнула."
    play music music_7dl["shehasgone"] fadein 5
    us "И правда, глупо было надеяться."
    "Она погрустнела."
    us "Что ты так легко меня простишь."
    us "Я просто пока ужинала, подумала… {w}Ну, знаешь…"
    us "Что была слишком жестокой. {w}Ты, конечно, новичок, и есть традиции."
    "Она старательно ковыряла сандалией бетонную плиту."
    us "Но все мы же люди, все делаем ошибки… Я и дала маху."
    show us shy2 sport with dspr
    us "В общем, дура я, что с едой шутить решила."
    us "Если бы я оказалась на твоём месте, даже не знаю, как бы сама поступила. {w}Но мне точно было бы обидно."
    us "Поэтому решила, что смогу исправиться."
    us "Как-то помочь тебе."
    show us shy sport with dspr
    "Она отвела глаза."
    us "Теперь понимаю, плохая была затея."
    "Вздохнула она."
    us "Я пойду, хорошо?"
    hide us with dissolve
    "Отвернувшись, девочка побрела прочь."
    "Вся её фигура выражала горечь и разочарование."
    "А у меня вдруг кольнуло в сердце от понимания, насколько я некрасиво и жестоко сейчас поступил."
    "Девочку замучила совесть, она решила исправиться, пусть и на свой манер, а я…"
    am "Эй…"
    "Улька остановилась."
    stop music fadeout 3
    show us sad sport far with dspr
    us "Что?"
    am "Ладно."
    "Я ещё пожалею об этом."
    am "Давай попробуем."
    play sound sfx_7dl["stahp"]
    pause(1)
    show us grin sport with flash
    play music music_7dl["genki"] fadein 3
    us "Правда?"
    menu:
        "Обойдёшься":
            if not alt_day1_me_d3_chase:
                show us calml sport with flash
                am "Если ты думала, что я забыл о нашем знакомстве, то ты напрасно так думала."
                am "Отвали."
            else:
                am "Возможно, если бы я нормально поужинал, всё было бы иначе."
                am "А сытый голодного не разумеет."
            return
        "Да, правда":
            $ alt_day2_us_candy = 1
            pass
    "Её настроение мгновенно скакнуло от подавленного до безоблачного."
    "Подозрительно мгновенно."
    "Я ещё не знал ни Ульяны, ни Алисы в должной мере, а слова единственного человека, к которому следовало бы прислушаться, по скудоумию своему, пропустил мимо ушей."
    "Хотя надо было, надо было сделать выводы."
    "Просто очень хотелось поверить в искреннее раскаяние маленькой егозы."
    "Болван, что ещё можно сказать."
    "Обратно отыгрывать в любом случае было уже поздно:"
    am "Так что делать будем?"
    show us laugh sport with dspr
    us "Что-что, столовую грабить, разумеется!"
    "Она увлекла меня за угол здания, откуда мы могли видеть проходящих, а они нас — нет."
    scene bg ext_dining_hall_away_sunset at zexitleft
    show us smile sport at cleft
    with dissolve
    "Тактично задвинув меня подальше, она встала у самого угла и в лучших традициях шутеров с укрытиями выглядывала оттуда, проводя разведку местности."
    "Угол был достаточно удобен в том плане, что сюда никто не шёл — видимо, в эту сторону нет ни жилых помещений, ни каких-либо других мест, представляющих интерес для пионеров."
    us "Если верить разведке, ужин заканчивается без пятнадцати девять."
    "С умным видом говорила она."
    "Я на секунду даже зауважал её."
    show us normal sport with dspr
    us "Потом пятнадцать минут работают дежурные по столовой, а где-то без пяти минут с кухни уходят повара."
    us "То есть у нас будет окно в пять минут на операцию, но надо будет отвлечь дежурных."
    us "Ты этим и займёшься, отвлечёшь их, а я в это время проникну в буфет и заберу всё необходимое."
    us "Всё понял?"
    am "А как я их отвлеку?"
    us "Я знаю? Ты большой, ты и думай, вон, башка какая."
    "И эта девочка только что чуть не плакала, прося прощения?!"
    am "Послушай ты, пас…"
    "Начал было я, но мне без долгих разговоров заткнули рот."
    show us upset sport with dspr
    us "Помолчи. Шляпа идёт."
    th "Шляпа?"
    "Я аккуратно выглянул поверх головы Ульки."
    "Там шла вожатая, Ольга Дмитриевна."
    "Почему «шляпа», было не очень понятно, но я не стал спорить."
    "Убрался за угол и медленно досчитал до десяти, давая вожатой время убраться прочь."
    with fade2
    show us normal sport with dspr
    us "Убралась!"
    if alt_day1_me_d3_chase:
        "Я посмотрел на табло смартфона: время было без восьми минут."
    us "Приготовились!"
    "Кивнула девочка."
    us "Сейчас выйдет женщина-колобок, это повариха."
    us "Пропускаем её и начинаем!"
    hide us with fade
    "А ещё несколько минут спустя Улька наступила мне на ногу, давая сигнал, и растворилась в сумерках."
    "Ещё несколько секунд — и она машет мне, на полшага выступая из густой тени крыльца."
    "Мысли заметались."
    th "Надо отвлечь дежурных, но как?"
    "Возле меня на земле лежала палка, выглядевшая вполне перспективно."
    menu:
        "Поднять палку":
            $ alt_day1_me_d3_us_robbed = True
            $ reput -= 10
            $ lp_us += 1
            "Палка была неплохой — в меру тяжёлой, в меру сухой, и не переломилась, когда я попробовал её на прочность."
            am "На старт…"
            scene bg ext_dining_hall_near_sunset with dissolve
            play sound sfx_7dl["metal_hit_on_metal"]
            "Подбежав к дверям столовой, я что было сил заколотил по ним подобранным орудием."
            "Грохот стоял страшный!"
            "Но тем не менее, спаренный стук башмаков по полу я уловил."
            "И, продолжая колотить орудием уже по стенам, водосливам, трубам и прочим грохочущим поверхностям, неторопливо побежал направо, в сторону подсобных помещений."
            voice "Эй, стой!"
            "Крикнули сзади."
            "Ага, так я и послушался."
            voices "Стой, кому говорят!"
            scene bg ext_dining_hall_near_sunset at fast_running
            with flash
            "За спиной послышался топот, и я припустил изо всех сил."
            "Кажется, дежурные клюнули на приманку."
            "Что-то мешало мне бежать, и я с недоумением посмотрел на палку, которую до сих пор сжимал в руке."
            "Бросил её назад."
            "Это же азы французского искусства эскейпинга — набросать под ноги преследователям кучу мешающего им хлама."
            "Ну всё верно же! Швейцарцы славны шоколадом и универсальными зубочистками, немцы педантичностью и перфекционизмом, итальянцы кухней."
            "А французы — умением стырить и свалить. Искусством уличного ливинга."
            "Или сбегинга? Свалинга?"
            dreamgirl "Паркура!" with vpunch
            "Удачно — одновременно произошли два события: дежурные растянулись на земле и со стороны входа в столовую послышался скрип двери."
            "Я захохотал и прибавил ходу!"
            "К счастью, место было уже знакомое, я сегодня здесь уже был."
            "Именно здесь меня сегодня встретила рыжая фашистка."
            "Не та рыжая, с которой я сейчас проворачиваю ограбление века, а другая."
            scene bg ext_square_sunset with dissolve
            "Отсюда уже рукой подать было до площади, и я занял одну из скамеек, приходя в себя."
            "Со стороны столовой доносились какие-то переговоры на повышенных тонах."
            "Я спрятал довольную улыбку."
            "Для полноты эффекта не хватало только, чтобы моя подельница нашла меня и отсыпала мне мою долю."
            "Вряд ли мы разминёмся – все пути в этом лагере ведут к Генде."
            stop music fadeout 3
            show blink
            "Я закрыл глаза."
            play music music_list["everyday_theme"] fadein 3
            play sound sfx_7dl["pouring"]
            scene bg ext_square_sunset
            show unblink
            "А открыл их, когда мне на колени посыпалось нечто твёрдое и не сказать чтобы сыпучее."
            show us laugh sport with dspr
            us "Не спи, замёрзнешь!"
            "Рассмеялась она."
            us "Ты хорошо сработал и полностью заслужил свою награду."
            "Только сейчас я понял, что именно она высыпала на меня."
            "Конфеты."
            "Часть их уже пылилась здесь же на земле, часть осталась у меня на коленях."
            am "Э… Ну… Спасибо, наверное."
            us "Обращайся!"
            hide us with easeoutleft
            "Она сложила кулёк и, спрятав его в карман, унеслась прочь."
            "Оставляя меня в одиночестве собирать трофеи."
            th "Тут слишком много, чтобы рассовать всё по карманам."
            th "И в руках всё это не унести."
            "Но не бросать же всё здесь!"
            sl "Чем занимаешься?"
            show sl normal pioneer with dspr
            "По закону подлости рядом остановилась Славя."
            "Помощница вожатой поняла всё с первого взгляда."
            th "Сейчас кто-то будет бедный."
            "С отчаянием обречённого понял я."
            am "Да тут, понимаешь…"
            sl "Понимаю."
            show sl smile pioneer with dspr
            th "Тётенька, только не по голове, она у меня и так сегодня больная!"
            sl "Ульяна, да?{w} Она?"
            menu:
                "Вообще мы вдвоём там были":
                    $ reput += 10
                    $ lp_sl += 1
                    $ lp_us += 1
                    "Признался я."
                    am "Я шумел и отвлекал, а Ульяна…"
                    show sl smile2 pioneer with dspr
                    sl "Серьёзно? Не ожидала от тебя такого."
                    "Улыбнулась девушка."
                    sl "Не то чтобы я совсем против таких проделок, но ты здесь всего лишь первый день, и…"
                    th "Начались нотации."
                    "С тоской подумал я."
                    th "И от кого — от пионерки вдвое меня моложе!"
                    "Но Славя меня удивила."
                    show sl serious pioneer with dspr
                    sl "Но мне понравилось, что ты не стал выгораживать себя."
                "А кто ж ещё":
                    "Уныло протянул я."
                    "От мелкой одни проблемы, одни неприятности."
                    "Наверное, будет лучше, если я буду держаться подальше от неё."
                    show sl serious pioneer with dspr
                    sl "Знаешь, если ты не хочешь, чтобы она тебя во что-то втянула, лучше держись меня или Ольги Дмитриевны."
                    "Серьёзно предупредила Славя."
                    "Она знала!"
                    "Видела или догадалась – не суть важно."
                    "Факт тот, что Славя была в курсе относительно моей роли в происходящем."
            show sl normal pioneer close with dspr
            "Она села рядом со мной и, автоматически сцапав конфету, развернула ту и отправила в рот."
            show sl smile pioneer close with dspr
            sl "Вкусно! Ульянка знает, что брать!"
            "Похвалила она."
            sl "Давай договоримся так – я не стану докладывать вожатой о происшедшем, а ты сейчас собираешь конфеты и возвращаешь их в столовую."
            sl "А на будущее, если вдруг у тебя какие-то проблемы, тебе голодно или ещё что-то такое, давай ты сначала обратишься ко мне или вожатой?"
            sl "Уверяю, голодать мы тебе не дадим."
            show sl normal pioneer with dspr
            "Она легко поднялась и помогла мне собрать все «халвичные», которые удалось обнаружить."
            am "И что теперь?"
            sl "Понесли их обратно в столовую."
            am "Но она же уже закрыта!"
            play sound sfx_keys_rattle
            "Девушка достала из кармана связку ключей:"
            sl "Я всё-таки помощница вожатой."
            sl "Заодно и посмотрим, чем тебя подкормить, раз ты такой голодный."
        "К чёрту":
            "Мы тут немного подумали, и я решил…"
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_U_reject:
    scene bg ext_dining_hall_away_sunset with dissolve
    "Я воочию представил себе, как вожатая выговаривает мне нечто вроде: не успел приехать, а уже такими вещами занимаешься! Стыдно, пионер!"
    "Не то чтобы меня когда-то заботили такие вещи, как чьё-то мнение, но слушать нотации в моём возрасте уже как-то даже и несолидно."
    hide us with fade
    "Так что я отвернулся и, проигнорировав возмущённые возгласы, направился куда глаза глядят."
    "За миллионы лет обитания перед монитором с чашкой «Обжирака» наготове я прекрасно научился договариваться с собственным желудком."
    "Как-нибудь до завтра дотерплю, а там, глядишь, и завтраком накормят."
    "Ну, или хотя бы посадят на попутку прочь отсюда."
    th "Хотя верилось в это с трудом, после списка-то."
    "В любом случае, я не собираюсь позволять мелкой втягивать меня в разные сомнительные мероприятия."
    $ reput += 20
    $ lp_us -= 1
    play music music_list["my_daily_life"]
    scene bg ext_square_sunset
    sl "Вовремя ты оттуда ушёл."
    "Раздался голос за спиной."
    "Вздрогнув, я обернулся."
    scene bg ext_square_sunset at zenterleft
    show sl normal pioneer at cleft
    with dissolve
    am "Славя! Ты меня напугала."
    sl "Ещё бы чуть-чуть, и попались бы начальнику лагеря."
    "Проигнорировав мою реплику, продолжила она."
    am "Здесь и начальник есть?"
    "С уважением поинтересовался я."
    show sl smile2 pioneer with dspr
    sl "А как же без начальника-то. {w}Должен быть кто-то, кто будет нести ответственность."
    sl "Я за тебя, Ольга Дмитриевна — за наш отряд, а начальник — за весь лагерь."
    am "Что ты здесь делаешь?"
    show sl shy pioneer with dspr
    sl "Я видела, что ужин у тебя не удался, но помочь не смогла."
    sl "Прости, очень срочно надо было бежать… По одному… заданию."
    "С трудом подбирая слова, сказала она."
    sl "Так что не смогла тебе помочь."
    show sl smile pioneer with dspr
    sl "Но теперь я освободилась. {w}Ты как, голодный?"
    am "Нет-нет, я в полном порядке!"
    "Не хватало ещё, чтобы со мной тут нянькались!"
    play sound sfx_stomach_growl
    "Но у моего желудка было своё мнение на этот счёт."
    show sl surprise pioneer with dspr
    sl "Да? {w}Ну, раз ты так говоришь…"
    sl "Просто хотела подкормить тебя немного."
    am "Да нет, я…"
    play sound sfx_stomach_growl
    show sl laugh pioneer with dspr
    sl "Пошли!"
    "Она потянула меня обратно к столовой."
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return

label alt_day1_me_d3_ev_A_S:
    scene bg ext_dining_hall_away_night with dissolve
    play ambience ambience_camp_center_night
    "Мы шли немного по другому маршруту, чуть обходя столовую со стороны, и, сказать по правде, если бы я не знал абсолютно точно, что это столовая, я бы её не узнал."
    "Не то чтобы у меня был прогрессирующий топографический кретинизм, но всё-таки."
    play sound sfx_7dl["pup_bark"]
    show sl normal pioneer with dissolve
    "Славя резко остановилась."
    sl "Слышал?"
    "Я пожал плечами."
    am "Я вообще много чего слышу."
    "В отличие от привычной мне светлой и тихой ночи северо-западных регионов, здесь было темно, душно и шумно."
    sl "И всё-таки я слышала, как…"
    "Она оборвала себя."
    show sl shy pioneer with dspr
    sl "Знаешь, иди пока к крыльцу, а я пока схожу посмотрю, что там за шум?"
    am "Давай вместе!"
    "Логично предложил я."
    am "Вместе не так страшно."
    show sl smile2 pioneer with dspr
    sl "Да я справлюсь, иди!"
    sl "Я просто посмотрю быстренько, и сразу тебя догоню."
    am "Как скажешь."
    th "И чего она стесняется? Сказала бы, что ей надо посетить весёлый домик, я бы понял."
    hide sl with dissolve
    "Я пожал плечами и поплёлся к крыльцу."
    play sound sfx_alisa_picklock
    "А на крыльце уже кто-то стоял и, судя по звукам, пытался взломать замок."
    th "Ульяна?"
    "Под ногами хрустнула ветка, и взломщик резко обернулся."
    scene bg ext_dining_hall_near_night at zentercenter
    show dv scared pioneer with dspr
    with dissolve
    "Нет, для Ульяны он был слишком крупным."
    am "Воруем?"
    show dv normal pioneer with dissolve
    dv "Это ты."
    am "Что украсть-то хотела? Там уже всё Улька стырила."
    if alt_day1_me_d3_us_robbed:
        dv "А я хочу проверить."
        am "А, ну удачи тогда."
        "Я отступил в сторонку и сложил руки на груди."
        "Алиса покосилась на меня, но я не отреагировал."
        show dv angry pioneer with dspr
        dv "Ты ничего сделать не хочешь?"
        am "Прости, я не в настроении танцевать и читать хайку…"
        dv "Что?"
        am "…а вот скоро сюда придёт Славя…"
        "Продолжил я, ослепительно улыбаясь:"
        am "…и она тебе с удовольствием спляшет и споёт."
        show dv rage pioneer with dspr
        dv "И ты молчал?!"
        dv "Ну дождёшься ты у меня."
        "Она мигом отскочила от двери."
    else:
        dv "Да ничего она не стырила, чуть не попалась начальнику лагеря."
        dv "А всё потому, что один придурок не захотел ей помочь."
        am "Точно, тогда мы бы попались оба."
        am "В баню вашу круговую поруку, я лучше буду со Славей общаться."
        am "Она хотя бы сейчас меня покормит."
        show dv angry pioneer with dspr
        dv "И она идёт сюда?"
        "Алиса тут же спрятала отмычки в карман и отскочила к лестнице."
    menu:
        "Помочь":
            $ lp_dv += 1
            $ reput += 10
            $ alt_day1_me_d3_dv_feed = True
            am "Не пробовала сделать всё нормальным путём?"
            "Устало спросил я."
            am "Подойти, там, поговорить."
            am "Обязательно вести себя как малолетняя преступница?"
            dv "Да что ты знаешь…"
            "Зашипела Алиса."
            "Я пожал плечами."
            am "Только то, что я мог бы тебе что-нибудь вынести – если ты попросишь как следует."
            show dv shocked pioneer with dspr
            dv "И быть тебе обязанной?"
            dv "Я пас!"
            hide dv with dissolve
            "Алиса скрылась в темноте, а я пожал плечами."
            "Колхоз — дело добровольное."
        "Не помогать":
            "А со стороны задних помещений уже был слышен звук шагов спешащей Слави."
            am "Поступь бронзового командора."
            "Усмехнулся я."
            "Отмахнувшись от моих слов, Алиса перескочила перила с дальней стороны крыльца и скрылась в сумерках."
            hide dv with dissolve
            am "Как говорится, попутного ветра в горбатую спину."
            "Хотя честно сказать, было огромное искушение посмотреть на то, как бы рыжая выкручивалась, попавшись с поличным."
            "Хотя бы отомстить за тазик."
            "Но такой вот я сегодня великодушный."
    with fade
    "Славя шла неторопливо, на её лице играла полуулыбка."
    "То ли мысли были хорошие, то ли что-то хорошее случилось за эти десять минут."
    show sl normal pioneer with dissolve
    am "Что там было?"
    sl "А, ничего особенного."
    "Отмахнулась она."
    sl "У тебя тут всё в порядке? {w}Мне показалось, что ты с кем-то спорил."
    am "Да нет, всё хорошо."
    "Я немного напряжённо улыбнулся."
    am "Сейчас меня покормят, что может быть лучше?"
    show sl laugh pioneer with dissolve
    "Славя рассмеялась и, поискав на связке, обнаружила нужный ключ, после чего отворила дверь в столовую."
    play sound sfx_open_door_clubs fadein 0
    sl "Заходи."
    "Она, кажется, не обратила внимания на мои актёрские потуги. {w}Или вежливо притворилась, что всё хорошо."
    scene bg int_dining_hall_night
    with dissolve
    play music music_list["a_promise_from_distant_days"] fadein 5
    play ambience ambience_dining_hall_empty fadein 3
    "В столовой мы прошли до самой раздачи, и Славя, усадив меня, скрылась в недрах кухни."
    show sl normal pioneer with dspr
    sl "Подожди здесь, ладно? Я тебе сейчас что-нибудь принесу."
    am "Бу сделано."
    "Я откинулся на спинку и стал ждать свою спасительницу."
    hide sl with dissolve
    scene cg d1_sl_dinner
    with dissolve
    "Через пару минут она появилась с большим блюдом булочек и поставила его на стол."
    "Потом на столе оказалось несколько старинных кефирных пирамидок — сто лет таких не пробовал!"
    sl "Там ещё кастрюлька с бульоном, но она совсем остыла."
    am "А и чёрт с ним. И так поем."
    sl "Приятного аппетита."
    am "Спасибо."
    "Не сказать чтобы ужин отличался разнообразием. Хотя не мне с моими дошираками воротить нос."
    scene cg d1_sl_dinner_0 with dspr
    "Пока я ел, Славя внимательно, изучающе разглядывала меня — так, что под конец ужина я даже почувствовал себя немного неловко."
    "Что вообще странно, этой девочке в считанные часы удалось подобрать ключик к моему сердцу, и я уже не считаю её посторонним прохожим."
    "Как ей это удалось? Может, дело в её отношении к окружающим?"
    am "У меня кефир на носу остался?"
    "Я потёр кончик носа, вызывая её смех."
    sl "Нет. Просто…"
    "Она улыбнулась."
    sl "Я хотела спросить, как тебе здесь, в лагере? Первый день."
    th "Первый ли?"
    am "Спорно."
    th "Не слишком красиво спрашивать у человека, которого за шкирку выдернули из родной эпохи и переместили лет на тридцать, где он даже не знает, как вести себя, как это ему нравится на новом месте…"
    sl "Ничего."
    "Она совершенно правильно расценила моё молчание."
    sl "Ещё привыкнешь."
    scene cg d1_sl_dinner with dspr
    "Славя мечтательно уставилась в окно, а я отрицательно покачал головой."
    th "Не думаю, что я когда-нибудь привыкну к этому месту."
    th "Впрочем, ей-то и невдомёк…"
    th "Или она хочет, чтобы я думал, что ей невдомёк. {w}И дальше играем в игру — я знаю, что она знает, что я знаю, что она знает."
    am "Я раньше был в лагерях, так что есть с чем сравнивать."
    "Надо было как-то прерывать повисшее молчание."
    sl "Правда?"
    "Без особого интереса спросила она."
    am "Правда."
    "Я чуть было не выдал нечто в духе «всю жизнь прокатался», но это не слишком удачно соотнеслось бы с тем, на сколько я выгляжу."
    "Она выглядела по-настоящему влюблённой в это место, и внутри шевельнулось что-то, не дающее остаться равнодушным или выдать нечто нейтральное."
    "Почему-то очень захотелось сделать или сказать что-нибудь, что заставило бы эту непосредственную девочку улыбнуться."
    am "И я хочу сказать, здесь очень…"
    sl "Как?"
    scene cg d1_sl_dinner_0 with dspr
    "Она отвернулась от окна и внимательно изучала меня. {w}Так, будто от моего ответа зависело что-то очень-очень важное."
    am "Здесь мирно."
    "Улыбнулся я."
    am "Как будто оказался в том месте, какое всегда искал."
    sl "Пожалуй."
    "Славя облегчённо улыбнулась, будто я правильно ответил на важный вопрос."
    sl "Я очень рада, что ты так думаешь."
    am "Почему?"
    sl "Ты знаешь… Ты почему-то очень хорошо вписываешься сюда."
    "Она с трудом подбирала слова."
    sl "Вот смотришь на тебя и понимаешь — вот это человек на своём месте."
    sl "Было у тебя такое?"
    "Я покачал головой."
    am "А тебе?"
    "Молчание стало нестерпимым, захотелось деть куда-то руки… Но чем есть тогда?"
    sl "Мне?"
    am "Нравится здесь?"
    sl "Я здесь отдыхаю."
    "Призналась она."
    am "Помощницей вожатой?"
    sl "Добровольной, да."
    "Славя улыбнулась."
    sl "Я привыкла постоянно быть чем-то занятой, а помощь вожатой… Скажем, она позволяет совмещать отдых и занятость."
    "Кажется, этот разговор уводил меня далеко-далеко, совершенно не туда, куда я стремился."
    sl "Ты волнуешься."
    "Это было сказано утвердительным тоном."
    am "Что? Почему?"
    sl "Ты жуёшь слишком сосредоточенно."
    am "Прости…"
    sl "Ничего."
    "Никак не получалось воспринимать её как кого-то чужого."
    "Хотя бы чуть больше осторожности в расспросах и ответах."
    "Я же язык жестов изучал, кое-что могу сказать по позе и осанке человека."
    "Но здесь и сейчас напротив меня сидел кто-то настолько искренний и открытый, что все попытки анализировать его заканчивались неиллюзорными угрызениями совести."
    "Как и большинство людей, которых я здесь встретил."
    th "Они честные и нормальные."
    th "Обычные люди в обычной обстановке с небольшой поправкой на седьмой-восьмой десяток двадцатого века."
    "Все они были {b}нормальными{/b} — такими, какими и должны быть люди в моём понимании, со своими достоинствами и недостатками."
    "А Славя была ещё и…{w} милой?"
    "Я украдкой посмотрел на неё, не зная что сказать, а она перехватила взгляд."
    scene cg d1_sl_dinner with dspr
    sl "Всё-таки поздно ты приехал, поздно."
    am "Почему ты так говоришь?"
    sl "Ну… Ощутимая часть смены уже позади, ты пропустил столько всего интересного."
    sl "Как мы знакомились, как привыкали – те, кто ездит сюда не первый год и новички."
    sl "Ульяна с Алисой здесь не впервые, сразу друг друга нашли. Остальным пришлось чуть похуже."
    am "А ты?"
    sl "Я сюда уже ездила, кое-кого знаю. Поэтому мне доверяют."
    sl "Лена тоже."
    sl "А Ольга Дмитриевна вообще здесь выросла."
    sl "Когда она за тобой уехала, пришлось мне исполнять обязанности вожатой."
    sl "А тут буря – поломала лагерную антенну, ребята хотели починить, но всё, видишь, некогда."
    th "И между пионерами в бурю и мной вожатая выбрала меня?"
    th "Странная она какая-то."
    sl "Ещё день Нептуна был, день Наоборот, олимпиада."
    sl "А в третий день, когда с утра шёл дождь, мы собрались в клубе, рассказывали всякие интересные вещи и передавали свечку."
    sl "Так был создан кружок кибернетики — Электроник и Шурик нашли общие интересы."
    am "Роботов, да?"
    sl "И их тоже."
    am "А что ещё?"
    sl "Да так. {w}Спросишь у них потом, если захочешь."
    sl "Из интересного остался только родительский день да прощальный концерт на закрытие смены."
    am "Это ничего. Я хорошо переношу скуку."
    sl "Ну уж заскучать мы тебе не дадим."
    "Она выглядела как человек, которому хотелось, чтобы вокруг него кипела деятельность, шумели голоса, бегали люди."
    "Чтобы чувствовалась жизнь."
    "Она любила жизнь всей душой, и глядя на неё поневоле хотелось стать таким же."
    sl "Начнём с пляжа, пожалуй. {w}Видел пляж?"
    "Я отрицательно покачал головой."
    "Но её идея мне понравилась. В конце концов, это достаточно яркое переживание."
    scene cg d1_sl_dinner_0  with dspr
    "Что-то, о чём хотя бы останется память?"
    th "Ну, это точно не сегодня — ночь на дворе."
    stop music fadeout 5
    stop ambience fadeout 2
    scene bg int_dining_hall_night
    show sl smile pioneer with dissolve
    "Она встала из-за стола."
    sl "Уже поздно. Сам дорогу до Ольги Дмитриевны найдёшь?"
    am "Конечно, найду. Но зачем мне к ней?"
    sl "Спать не собираешься?"
    am "Собираюсь, но…"
    show sl laugh pioneer with dspr
    "Наверное, со стороны я выглядел действительно крайне глупо, потому что Славя рассмеялась."
    sl "Или забыл, куда тебя поселили?"
    am "Да я как бы не совсем в курсе."
    sl "О… Тогда для тебя это будет сюрпризом."
    show sl smile pioneer with dspr
    sl "Ладно, я побежала тогда.{w} Спокойной ночи!"
    hide sl with dissolve
    am "Сладких…"
    "Я начал собираться в домик, прихватил с собой остатки пиршества — несколько булочек и пирамидки с кефиром. Схомячу ближе к ночи."
    th "Странно, почему это Славя так внезапно…"
    "Свет фонаря с улицы отразился в чём-то металлическом, висящем на двери."
    th "Славина связка ключей! {w}Как это она такую вещь важную забыла?"
    "Я было собрался догонять спасительницу, но в какую сторону догонять, не догадался."
    "А стучаться во все домики подряд среди ночи показалось не самой разумной затеей."
    scene bg ext_dining_hall_near_night with dissolve
    play ambience ambience_camp_center_night fadein 5
    "Ночь была… Необычной. Нет, не так. Она была непривычной."
    "Мне, человеку, прожившему всю жизнь в северо-западном округе и всем другим видам отдыха предпочитавшему Карельский перешеек, были диковаты и необычайная темнота, и влажный воздух, и стрекот сверчков."
    th "Северная летняя ночь куда тише. А эта меня пугает."
    "Надо срочно последовать совету Слави и добраться до домика вожатой."
    th "Пока на меня не напали и не схарчили местные ночные обитатели."
    if alt_day1_me_d3_dv_feed:
        play music music_list["orchid"]
        play sound sfx_hiding_in_bush
        "Я проверил дверь в столовую ещё раз, и тут в кустах за спиной что-то зашуршало, под чьим-то весом ощутимо хрустнула ветка."
        "Мне стало {b}очень{/b} не по себе."
        "Булочки поместились в кармане, а вот кефир пришлось отложить на стоящую тут же скамеечку."
        "Распределив добычу, я стал оглядываться в поисках подходящего оружия."
        with vpunch
        play sound sfx_scary_sting
        "И тут меня кто-то похлопал по плечу."
        "Это было довольно неожиданно, и, кажется, реакция моя была не самой вменяемой."
        am "Ааааа, машу вать, кто здесь?! Не подходите, я буду драться!"
        "Я отскочил в сторону и резко обернулся. Там стояла…"
        stop music fadeout 1
        show dv laugh pioneer with dspr
        am "Алиса? Ты меня угробить решила?! Тебе чего надо?"
        "Она чуть ли не в голос хохотала, довольная результатами своей проделки."
        dv "Это не я чего, это ты чего разорался? {w}Принёс еду?"
        am "Едуууу?"
        "Переспросил я, борясь с нарастающим желанием придушить мерзавку."
        am "Ты же сказала, что не хочешь быть в долгу!"
        show dv smile pioneer with dspr
        dv "Я сказала — я передумала. Так принёс?"
        am "Две булочки с изюмом устроят?"
        show dv shy pioneer with dspr
        dv "Ну ты даёшь. А где всё остальное?"
        am "СЪЕЛ!"
        am "Я не ужинал, помнишь?"
        dv "Ладно, что-нибудь ещё есть?"
        am "Полтора литра кефира хватит?"
        show dv normal pioneer with dspr
        dv "О, давай."
        "Пирамидки мгновенно исчезли."
        am "Могла бы и предупредить… Что всё-таки примешь предложение."
        "Я помассировал гудящие виски. От происходящего у меня жутко разболелась голова."
        am "И не подкрадываться так."
        show dv smile pioneer with dspr
        dv "Ну извини, не знала, что ты такой трус. А покормить меня была твоя идея. А принёс непонятно что."
 
 




        am "Что-нибудь ещё?"
        dv "Нет, ничего…"
        show dv grin pioneer with dspr
        dv "Иди спи. Спокойной ночи, трус."
        am "Бывай."
        hide dv with dissolve
        "Если Алисе в ту сторону, значит, мне в диаметрально противоположную."
    stop music fadeout 4
    stop ambience fadeout 3
    with fade
    return

label alt_day1_me_d3_ev_mt:
    scene bg ext_square_night with dissolve
    play music music_7dl["melancholy_sun"] fadein 3
    "Здесь пахнет рекой и несбыточными мечтами."
    "Здесь, среди сосен, впитавших солнечное тепло и детское счастье, можно на секунду поверить в то, что не было никогда её — моей глупой жизни."
    "А всегда было так."
    "Тепло, уютно и безмятежно."
    "Я не вспомнил всех, но вспомнил то, что они во мне вызывали."
    "Тараторка."
    "Злой ангел."
    "Глубина глаз и золото волос."
    "Миры по ту сторону холста."
    "Миры эмоций, которые я никогда не заслуживал, но любил всей душой."
    if alt_day2_us_candy == 1:
        "Наверное, если бы мы не побежали тогда грабить столовую, и она не приняла на себя командование… {w}Я бы не вспомнил."
    "Но теперь же…"
    th "Сестра."
    "Улыбнулся я."
    th "Ты и в юности была такой же балбеской."
    "И, конечно, моя вечная мучительница, что однажды обнаружит меня на залитых дождём улицах Петербурга."
    "У каждой из них есть своя история."
    "Своё прошлое, отсюда кажущееся сумрачным и почти несбыточным."
    "И, уверен, от одного меня лишь зависит, станет ли оно когда-нибудь настоящим."
    "Сейчас самое главное — это определиться с моим местом."
    "И начинать работать."
    "С юга слышался звонкий смех двух рыжих подружек-сестёр."
    "С севера ветер доносил раскатистый «пум-пурум-пум»."
    "Я почувствовал, что улыбаюсь — необъяснимо, глупо и счастливо."
    "Теперь всё зависит от одного меня."
    "Я обязательно всё исправлю."
    "Обязательно."
    scene bg ext_houses_night_7dl
    with dissolve
    "К моменту, когда я добрался до жилища вожатой и своего будущего места обитания, все уже разбрелись по домикам."
    "Лагерь ворочался, устраиваясь на ночлег."
    "Значит, пора и мне?"
    th "Ну, что… Тётя Оля?"
    scene bg ext_house_of_mt_night
    with dissolve
    pause(2)
    play sound sfx_knock_door2
    mt "Открыто!"
    play sound sfx_open_dooor_campus_1
    scene bg int_house_of_mt_noitem_night
    show mt smile sport
    with fade
    pause(1)
    play ambience ambience_int_cabin_night fadein 1
    am "Добрый вечер."
    "Ольга Дмитриевна приветливо улыбнулась мне."
    "И на каком-то подспудном уровне я уже знал, что она сейчас скажет, что предложит."
    "Хотя будь моя воля, я бы поселился по соседству с…"
    "Дух захватывает от мысли о перспективах!"
    "Ни одна из них ещё меня не знает, ни одной из них я не успел испортить жизнь."
    "Но и рассказывать обо всём я не имею права."
    "Зато…"
    show mt normal sport with dspr
    mt "Успокойся."
    "Я могу увидеть товарища начальника ещё до того, как она истосковалась в ожидании."
    mt "Поселишься пока со мной, я и бельё тебе добыла."
    "Заглянуть в глаза старшей сестре, пока она была ещё проказницей-сорванцом."
    mt "Ты, как и всегда, прибыл без вещей. Охохонюшки…"
    mt "Ничего, подберём что-нибудь под размер."
    "Я могу полюбоваться Алиской! Она, кстати, не сильно и изменилась с тех самых пор, как…"
    th "Чёрт, как бы это сказать — до того, как с ней случится нечто плохое?"
    "Кошмар натуральный."
    show mt grin sport with dspr
    mt "Вопросы уже не задаёшь, освоился."
    mt "Это хорошо."
    "Она сложила ежедневник и убрала на полку."
    am "А что спрашивать-то… {w}И так всё понятно."
    show mt sad sport with dspr
    mt "Совсем-совсем всё понятно?"
    "Она очень странно посмотрела на меня."
    "Но уж извините, тётя Оля — здесь я совсем не за вами, и я уж точно не тот, кого вы так долго пытались вытянуть."
    mt "Понятно. {w}Что ж, застилайся и ложись спать."
    mt "Завтра будет очень насыщенный день."
    show mt smile sport with dspr
    "Блеснули глаза юной девчонки, опустилось веко в мимолётном подмигивании — и будто привиделась мне эти её тоска и меланхолия."
    "Будто не было никогда в её жизни Полынь-звезды, госбезопасности и прибывшего на автобусе новичка."
    mt "Падай баю. {w}Гуте нахт."
    mt "Я на обход, а ты устраивайся."
    play sound sfx_click_2
    scene bg int_house_of_mt_night2 with dissolve
    "Она щёлкнула выключателем."
    "Негромко хлопнула дверь."
    "И я остался один."
    "Наедине со своими надеждами, планами, фантазиями."
    scene cg d1_end_of_day_7dl with fade
    am "Я спасу вас, слышите? {w}Чего бы мне это ни стоило."
    "Пообещал я потолку."
    "И закрыл глаза."
    stop music fadeout 4
    stop ambience fadeout 6
    return

label alt_day2_me_d3_pre:
    play music music_7dl["let_me_down"] fadein 3
    scene
    $ renpy.show("bg int_house_of_mt_sunset", what = Desat("bg int_house_of_mt_sunset"))
    with dissolve
    play ambience ambience_int_cabin_night fadein 3
    "Тоска."
    "Обычно говорят, что такая тоска зелёная, но в действительности она была серой и никакой иной."
    "Ещё вчера я уверял себя в том, что могу справиться с чем угодно, что теперь-то я знаю наперёд."
    "А оказалось…"
    "Я приоткрыл глаз и покосился на соседнюю кровать — та уже была пуста."
    "Тётю Олю я помнил усталой и немолодой, отчаявшейся в своих поисках."
    "Эта же была юной девчонкой, к которой ну никак не получалось отнестись серьёзно."
    "Она вся была такая попрыгучая, смеющаяся, радостная — как надутый веселящим газом шарик."
    "Вот только время способно выключить из жизни даже самый жизнерадостный шарик."
    "Пройдёт ещё десять, двадцать лет…"
    "Или нет."
    "Если я буду действовать успешно, если учту всё и сделаю всё от меня зависящее — в её глазах никогда не погаснет огонёк счастья."
    "Пусть и скрываемый успешно её показной ленью."
    "Но она уже ушла."
    "Опять кряхтела и скрипела на кровати недовольным голосом, нехотя ставила ногу на пол."
    "Откуда-то я это знал, пусть и спал всё время до того, как хлопнула дверь."
    "А теперь я остался один."
    "Погасло само эхо от хлопка, даже послевкусия эха — можно уже было начинать сомневаться, а был ли вообще звук?"
    "Или я так всегда и лежал один под тонким одеялом, таращась в льющуюся из окон серость?"
    "И не было никогда ни девочек, ни прошлого моего из пальца высосанного."
    "Ни-че-го."
    "Панцирная сетка недовольно взвизгнула, когда я понинул её объятья, запела тонко, задребезжала."
    if not persistent.me_neu_true:
        "Выдохнула. Как будто выдохнул весь мир."
        "И я вместе с ним."
        "Всё вокруг было как-то странно."
        "Непонятно."
        "Некорректно."
        "Будто бы я забыл сделать что-то крайне важное, держал, держал его в голове, но…"
        th "Пинг."
        dreamgirl "Заткнись."
        th "Ладно, пойду за таблетками к Виоле."
        "Вздохнул я."
        dreamgirl "Эй, эй! {w}Не стоит пороть горячку!"
        dreamgirl "Что тебе надо?"
        th "Ты всегда знаешь это раньше меня. {w}Зачем сейчас придуриваешься?"
        dreamgirl "Мало ли? Вдруг ты сам не знаешь, чего хочешь?"
        th "Мне нужен ответ на вопрос: что я забыл?"
        dreamgirl "Ты дурак, что ли?"
        "Не скрываясь, фыркнул внутренний голос."
        dreamgirl "Ты забыл больше девяноста процентов собственного прошлого. {w}Что именно тебя интересует?"
        th "Что я мог забыть касаемо текущего момента?"
        "Переругиваясь с вечным собеседником, я путался в рукавах, штанинах, ремешках…"
        dreamgirl "А почему бы тебе не посмотреть вокруг."
        "Голос рассмеялся и смолк."
        "Явно нарывался на розовую таблетку."
        th "Что ж поделать, придётся, видимо, навестить медсестру."
        "Проходя мимо шкафа, я задел его плечом."
        "Сверху что-то загрохотало, мне на голову посыпались какие-то тряпки."
        "Больно стукнуло тугим бумажным свёртком, перетянутым алой ниткой посередине."
        "От падения узел развязался, а с пола на меня уставился пионер, выполненый в стилистике плакатов Дмитрия Моора."
        "С плаката на меня сурово взирал пионер… Нет, не так."
        "С плаката на меня сурово взирал Пионер!"
        "Он указывал в мою сторону пальцем и непримиримо интересовался — а побывал ли я на сто первом этаже?"
        "Не про какой сто первый этаж я слыхом не слыхивал, да и глупо это было — искать здание такой высоты в пионерском лагере."
        th "Должно быть, для какой-то постановки."
        scene bg int_house_of_mt_sunset
        with dissolve
        "Заключил я и, свернув плакат, убрал его обратно на шкаф."
        "И заколебался:"
        th "А вдруг это не просто так?"
        "Вселенная никогда не подкидывала мне знаки от нечего делать, каждый из них что-то значил."
        "И за спиной у Пионера уж больно знаково алели символы молнии, ноты и солнца."
        th "Может…"
        dreamgirl "Может…"
        th "Что?"
        dreamgirl "Знаешь, что-то такое со сто первым этажом есть у меня в памяти, но смутно, расплывчато."
        dreamgirl "Так что ты бы подумал как следует. {w}Раз пять."
        th "Так и сделаю."
        "Я задумался."
        menu:
            "Вернуться?":
                stop music fadeout 2
                stop ambience fadeout 2
                with fade
                $ renpy.block_rollback()
                scene black
                with fade
                jump alt_day2_me_d3_start
            "Да ну, бред какой-то!":
                "Я толкнул дверь на улицу."
                scene bg ext_house_of_mt_sunset
                show uv rage with dissolve
                with flash
                "А там меня уже ждали."
                "Та самая…"
                uv "Что ты здесь делаешь?"
                am "Я…"
                uv "Что?!" with vpunch
                am "Нет уж, что это ТЫ здесь делаешь?"
                show uv sad with dspr
                uv "Спасаю тебя, дурака."
                "Как-то сразу и вдруг успокоилась девушка."
                uv "Похоже, что ты так ничего не понял, ничему не научился."
                uv "В какой раз ты здесь?"
                am "В первый."
                uv "Так и знала. Рано тебе ещё сюда, глупый Дём."
                uv "Рааано."
                uv "А потому…"
                scene
                $ renpy.show("bg ext_house_of_mt_sunset", what = Noir("bg ext_house_of_mt_sunset"))
                with dissolve
                "Она подняла над головой руку, и лагерь схлопнулся до состояния бесцветного каркаса, а там и до картинки на холсте."
                "До плоского изо…"
                scene cg d3_fag_room_7dl:
                    xalign 0.5 yalign 0.5 zoom 1.0
                    linear 2.0 zoom 1.5 xalign 0.5 yalign 0.35
                show prologue_dream
                with fade
                "До экрана монитора."
                uv "Добро пожаловать в реальный мир, Дём."
                uv "В этот раз попробуй сделать всё правильно."
                hide prologue_dream
                "Я заорал, чувствуя, как заживо крадут из меня все неслучившиеся минутки, как становится несбыточным всё, до чего я почти дотянулся."
                am "Эй, отдайте!"
                am "Это всё моё…"
                me "Моё!.."
    else:
        "Да только некогда мне было спать."
        "Как удалось понять по обрывкам чужой памяти, у меня всего семь дней на то, чтобы успеть сделать всё."
        "Семь дней лета."
        "А потому — наружу! {w}На улицу!"
        play sound sfx_open_dooor_campus_1
        pause(1)
        scene white with flash
    stop music fadeout 4
    stop ambience fadeout 6
    with fade
    return